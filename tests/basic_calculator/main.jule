// Copyright 2022-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use fmt for std::fmt
use conv for std::conv
use io for std::io

fn readln(): str {
    let scanner = io::Scanner.newf(io::stdin())
    if (scanner.scan() else { use false }) {
        ret scanner.text()
    }
    ret ""
}

fn numeric_input(msg: str)!: f64 {
    fmt::print(msg)
    let input = readln()
    ret conv::parse_float(input, 64) else { error(false) }
}

fn main() {
    for {
        let l = numeric_input("Input left: ") else {
            fmt::println("Invalid left!")
            continue
        }
        let r = numeric_input("Input right: ") else {
            fmt::println("Invalid right!")
            continue
        }
        fmt::print("Choose your op: + - / *\nYour op: ")
        let input = readln()
        match input {
        | "+":
            fmt::println(l + r)
        | "-":
            fmt::println(l - r)
        | "*":
            fmt::println(l * r)
        | "/":
            fmt::println(l / r)
        |:
            fmt::println("Invalid operation!")
        }
    }
}
