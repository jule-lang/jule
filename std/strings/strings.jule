// Copyright 2023 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Returns string that equals to concatenation of n-count s.
// Returns empty string is n <= 0.
pub fn repeat(s: str, mut n: int): str {
    if n <= 0 {
        ret ""
    }

    let mut ss = s
    for n > 1; n-- {
        ss += s
    }
    ret ss
}

// Reports string has prefix as specified substring or not.
pub fn has_prefix(s: str, sub: str): bool {
    if sub.len == 0 || s.len < sub.len {
        ret false
    }

    let mut i = 0
    for i < sub.len; i++ {
        if s[i] != sub[i] {
            ret false
        }
    }
    ret true
}

// Reports string has suffix as specified substring or not.
pub fn has_suffix(s: str, sub: str): bool {
    if sub.len == 0 || s.len < sub.len {
        ret false
    }

    let mut i = 0
    for i < sub.len; i++ {
        if s[s.len-i-1] != sub[sub.len-i-1] {
            ret false
        }
    }
    ret true
}
