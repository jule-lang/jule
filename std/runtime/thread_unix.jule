// Copyright 2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use integ "std/jule/integrated"

cpp use "<pthread.h>"
cpp use "<sched.h>"

cpp unsafe fn pthread_create(*cpp.pthread_t, *unsafe, *unsafe, *unsafe): int
cpp fn pthread_detach(cpp.pthread_t): int
cpp fn sched_yield(): int

#typedef
cpp struct pthread_t{}

unsafe fn _coSpawn(func: *unsafe, args: *unsafe): bool {
	let t: cpp.pthread_t
	if cpp.pthread_create(&t, nil, integ::Emit[*unsafe]("(void*(*)(void*))({})", func), args) != 0 {
		ret false
	}
	cpp.pthread_detach(t)
	ret true
}

fn osyield() {
	// Maximum attemp count to switch thread.
	// This is the total number of attempts.
	const AttempCount = 4
	if cpp.sched_yield() == 0 {
		ret
	}
	mut n := 1
	for n < AttempCount; n++ {
		if cpp.sched_yield() == 0 {
			ret
		}
	}
	panic("runtime: thread scheduling error")
}