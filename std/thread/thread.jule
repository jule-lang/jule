// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

cpp use "thread.hpp"

#namespace "std"
#typedef
cpp struct thread {
    detach:   fn()
    join:     fn()
    joinable: fn(): bool
}

#typedef
cpp struct __jule_thread_handle {
    thread:    fn(): *cpp.thread
    ref_count: fn(): uint
    drop:      fn()
}

cpp fn __jule_spawn_thread(routine: fn()): cpp.__jule_thread_handle

// Thread is a wrapper structure for native threads.
// It uses C++ threads in itself. It automatically detaches when destroyed.
// It supports the use of common interface. For this, it allocates
// the related thread object on the heap and provides memory safety
// with reference counting. It is destroyed when the internal thread
// reference count reaches to zero. In assignment operations, all
// thread instances point to the same allocation.
//
// It is experimental.
struct Thread {
    handle: cpp.__jule_thread_handle
}

impl Thread {
    // Spawns new thread by routine.
    // Panics if routine is nil.
    // Thread starts execution of routine when spawned.
    static fn Spawn(routine: fn()): &Thread {
        if routine == nil {
            panic("std::thread Thread.Spawn: routine is nil")
        }
        ret &Thread{
            handle: cpp.__jule_spawn_thread(routine),
        }
    }
}

impl Thread {
    // Wait for complete execution of thread if active.
    // Returns when the thread execution has completed.
    // So program stops execution until thread's execution has completed.
    //
    // Panics if thread is not active.
    fn Wait(self) {
        if !self.Active() {
            panic("std::thread Thread.Wait: thread is not active")
        }
        unsafe { self.handle.thread().join() }
        self.handle.drop()
    }

    // Detach thread and make it independent.
    // After this, this thread object can no longer identify thread.
    fn Detach(self) {
        if !self.Active() {
            panic("std::thread Thread.Detach: thread is not active")
        }
        unsafe { self.handle.thread().detach() }
        self.handle.drop()
    }

    // Reports whether thread object identifies an active
    // thread of execution.
    fn Active(self): bool {
        if self.handle.thread() == nil {
            ret false
        }
        ret unsafe { self.handle.thread().joinable() }
    }

    // Detaches thread if thread is still executing.
    fn Dispose(mut self) {
        if self.Active() && self.handle.ref_count() == 1 {
            unsafe { self.handle.thread().detach() }
        }
        self.handle.drop()
    }
}