// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/unsafe"
use "std/fs"
use "std/io"
use "std/internal/fmt"
use "std/internal/strings"

// Prints arguments to file by default formatting.
// See documentation of format function for formatting.
fn Fprint(mut f: &fs::File, args: ...any) {
	mut sb := strings::StrBuilder.New(20)
	for _, arg in args {
		fmt::FmtByDefault(sb, arg)
		f.Write(unsafe { sb.Buf() }) else {
			panic("std::fmt: Fprint: error occurs when printing")
		}
		// Do not use the [Clear] method to avoid making new allocations.
		// The buffer used temporarily, so just clear the length, not capacity.
		unsafe { sb.SetBuf(sb.Buf()[:0]) }
	}
}

// Prints arguments to file by default formatting.
// Prints new-line after arguments.
// See documentation of format function for formatting.
fn Fprintln(mut f: &fs::File, args: ...any) {
	Fprint(f, args...)
	Fprintf(f, "\n")
}

// Prints result of formatting to file.
// See documentation of format function for formatting.
fn Fprintf(mut f: &fs::File, fmt: str, args: ...any) {
	format := fmt::Format(fmt, args...)
	f.Write(format) else {
		panic("std::fmt: Fprintf: error occurs when printing")
	}
}

// Prints result of formatting to stdout.
// See documentation of format function for formatting.
fn Printf(fmt: str, args: ...any) {
	Fprintf(io::Stdout().File(), fmt, args...)
}

// Prints arguments by default formatting to stdout.
fn Print(args: ...any) {
	Fprint(io::Stdout().File(), args...)
}

// Prints arguments by default formatting to stdout.
// Prints new-line after arguments.
fn Println(args: ...any) {
	Fprintln(io::Stdout().File(), args...)
}

// Returns string result of arguments by default formatting.
fn Sprint(args: ...any): str {
	mut buf := strings::StrBuilder.New(100)
	for _, arg in args {
		fmt::FmtByDefault(buf, arg)
	}
	ret buf.Str()
}