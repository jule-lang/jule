// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/fmt"
use "std/internal/stringslite"
use "std/os"
use "std/unsafe"

// Prints arguments to file by default formatting.
// See documentation of format function for formatting.
fn Fprint(mut f: &os::File, args: ...any) {
	mut sb := stringslite::Builder{}
	sb.Grow(20)
	for _, arg in args {
		fmt::FmtByDefault(sb, arg)
		f.Write(unsafe { sb.Buf() }) else {
			panic("fmt: Fprint: error occurs when printing")
		}
		// Do not use the [Clear] method to avoid making new allocations.
		// The buffer used temporarily, so just clear the length, not capacity.
		unsafe { sb.SetBuf(sb.Buf()[:0]) }
	}
}

// Prints arguments to file by default formatting.
// Prints new-line after arguments.
// See documentation of format function for formatting.
fn Fprintln(mut f: &os::File, args: ...any) {
	Fprint(f, args...)
	Fprintf(f, "\n")
}

// Prints result of formatting to file.
// See documentation of format function for formatting.
fn Fprintf(mut f: &os::File, fmt: str, args: ...any) {
	format := fmt::Format(fmt, args...)
	f.Write(format) else {
		panic("fmt: Fprintf: error occurs when printing")
	}
}

// Prints result of formatting to stdout.
// See documentation of format function for formatting.
fn Printf(fmt: str, args: ...any) {
	Fprintf(unsafe { os::Stdout().File() }, fmt, args...)
}

// Prints arguments by default formatting to stdout.
fn Print(args: ...any) {
	Fprint(unsafe { os::Stdout().File() }, args...)
}

// Prints arguments by default formatting to stdout.
// Prints new-line after arguments.
fn Println(args: ...any) {
	Fprintln(unsafe { os::Stdout().File() }, args...)
}

// Returns string result of arguments by default formatting.
fn Sprint(args: ...any): str {
	mut buf := stringslite::Builder{}
	buf.Grow(100)
	for _, arg in args {
		fmt::FmtByDefault(buf, arg)
	}
	ret buf.Str()
}