// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::fs::{File}
use io for std::io
use nosafe for std::internal::nosafe

// Prints arguments to file by default formatting.
// See documentation of format function for formatting.
fn Fprint(mut f: &File, args: ...any) {
    let mut buf = make(str, 20)
    for _, arg in args {
        fmtByDefault(buf, arg)
        f.Write(nosafe::Stobs(buf)) else {
            panic("std::fmt: Fprint: error occurs when printing")
        }
        nosafe::ClearStr(buf)
    }
}

// Prints arguments to file by default formatting.
// Prints new-line after arguments.
// See documentation of format function for formatting.
fn Fprintln(mut f: &File, args: ...any) {
    Fprint(f, args...)
    Fprintf(f, "\n")
}

// Prints result of formatting to file.
// See documentation of format function for formatting.
fn Fprintf(mut f: &File, fmt: str, args: ...any) {
    let format = format(fmt, args...)
    f.Write(nosafe::Stobs(format)) else {
        panic("std::fmt: Fprintf: error occurs when printing")
    }
}

// Prints result of formatting to stdout.
// See documentation of format function for formatting.
fn Printf(fmt: str, args: ...any) {
    Fprintf(io::Stdout().File(), fmt, args...)
}

// Prints arguments by default formatting to stdout.
fn Print(args: ...any) {
    Fprint(io::Stdout().File(), args...)
}

// Prints arguments by default formatting to stdout.
// Prints new-line after arguments.
fn Println(args: ...any) {
    Fprintln(io::Stdout().File(), args...)
}

// Returns string result of arguments by default formatting.
fn Sprint(args: ...any): str {
    let mut buf = make(str, 100)
    for _, arg in args {
        fmtByDefault(buf, arg)
    }
    ret buf
}