// Copyright 2023 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use conv for std::conv
use integrated for std::jule::integrated
use utf8 for std::unicode::utf8

fn find_format_prefix(&bytes: []byte, &start: int): int {
    let mut i = start
    for i < bytes.len {
        let (r, size) = utf8::decode_rune(bytes[i:])
        if r == '{' {
            ret i
        }
        i += size
    }
    ret -1
}

// Returns range of {...}, included braces.
// Parameter i is the start point to selected and should
// points to start brace at beginning.
// i is points to position of last brace and +1 offset in the end.
// Returns nil slice if there is no match.
pub fn get_format_range(mut &i: int, mut &bytes: []byte): []byte {
    let start = i
    let mut braces = 0
    for i < bytes.len {
        let (r, size) = utf8::decode_rune(bytes[i:])
        i += size
        match r {
        | '{':
            braces++

        | '}':
            braces--
            if braces == 0 {
                ret bytes[start:i]
            }
        }
    }
    ret nil
}

fn fmt_by_default(&arg: any): []byte {
    if arg == nil {
        ret ['n', 'i', 'l']
    }

    match type arg {
    | f32: ret []byte(conv::fmt_float(f32(arg), 'g', -1, 32))
    | f64: ret []byte(conv::fmt_float(f64(arg), 'g', -1, 64))
    | i8:  ret []byte(conv::fmt_int(i8(arg), 10))
    | i16: ret []byte(conv::fmt_int(i16(arg), 10))
    | i32: ret []byte(conv::fmt_int(i32(arg), 10))
    | i64: ret []byte(conv::fmt_int(i64(arg), 10))
    | u8:  ret []byte(conv::fmt_uint(u8(arg), 10))
    | u16: ret []byte(conv::fmt_uint(u16(arg), 10))
    | u32: ret []byte(conv::fmt_uint(u32(arg), 10))
    | u64: ret []byte(conv::fmt_uint(u64(arg), 10))
    | str: ret []byte(str(arg))

    | bool:
        if bool(arg) {
            ret ['t', 'r', 'u', 'e']
        }
        ret ['f', 'a', 'l', 's', 'e']

    |:
        ret []byte(integrated::to_str(arg))
    }
}

fn apply_fmt_by_default(mut &j: int, &args: ...any): []byte {
    let arg = args[j]
    j++
    ret fmt_by_default(arg)
}

// Returns result of formatting.
// Parameter j is the position of argument list.
fn apply_fmt(mut &fmt: []byte, mut &j: int, &args: ...any): []byte {
    // {}
    if fmt.len == 2 {
        ret apply_fmt_by_default(j, args...)
    }

    // {{}} = {}
    if fmt.len == 4 && fmt == ['{', '{', '}', '}'] {
        ret ['{', '}']
    }

    ret fmt
}

fn format_bytes(fmt: str, args: ...any): []byte {
    let mut fmt_bytes = ([]byte)(fmt)

    if args.len == 0 {
        ret fmt_bytes
    }

    let mut bytes = make([]byte, 0, fmt.len)

    let mut j = 0 // Args position.
    let mut last = 0
    let mut i = find_format_prefix(fmt_bytes, last)
    for i != -1; i = find_format_prefix(fmt_bytes, i) {
        bytes = append(bytes, fmt_bytes[last:i]...)
        let mut format = get_format_range(i, fmt_bytes)
        if format == nil {
            continue
        }

        bytes = append(bytes, apply_fmt(format, j, args...)...)
        if j >= args.len {
            bytes = append(bytes, fmt_bytes[i:]...)
            last = fmt_bytes.len
            break
        }

        // Set last positioning to latest value of i.
        last = i
    }

    if last < fmt_bytes.len {
        bytes = append(bytes, fmt_bytes[last:]...)
    }

    ret bytes
}

// It places the passes arguments in the string relative to the corresponding
// format string. The to_str function provided by the API is used for string
// conversion. Returns format string if args.len == 0. If the arguments
// have ended, the remaining part of format string is not processed and
// is returned as is.
//
// Formatting:
//  Arguments are processed sequentially. That is, when an argument
//  encounters a format string parameter, it will be processed according
//  to how many parameters it is. The 5th parameter uses the 5th argument
//  as the value.
//
//  Each format parameter is represented as "{}" in the format string.
//  These parameters will then be deleted according to the processing
//  algorithm and replaced with arguments.
//
//  The parameter "{{}}" is formatted as "{}" actually,
//  And does not increase argument list offset.
//
// Examples:
//  - format("{} {}!", "Hello", "World") = "Hello World!"
//  - format("{} {}") = "{} {}"
//  - format("{} is the {}", "PI Number") = "PI Number is the {}"
pub fn format(fmt: str, args: ...any): str {
    ret str(format_bytes(fmt, args...))
}
