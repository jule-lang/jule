// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use conv for std::conv
use integrated for std::jule::integrated
use utf8 for std::unicode::utf8

fn findFormatPrefix(&bytes: []byte, &start: int): int {
    let mut i = start
    for i < bytes.len {
        let (r, size) = utf8::DecodeRune(bytes[i:])
        if r == '{' {
            ret i
        }
        i += size
    }
    ret -1
}

// Returns range of {...}, included braces.
// Parameter i is the start point to selected and should
// points to start brace at beginning.
// i is points to position of last brace and +1 offset in the end.
// Returns nil slice if there is no match.
fn getFormatRange(mut &i: int, mut &bytes: []byte): []byte {
    let start = i
    let mut braces = 0
    for i < bytes.len {
        let (r, size) = utf8::DecodeRune(bytes[i:])
        i += size
        match r {
        | '{':
            braces++
        | '}':
            braces--
            if braces == 0 {
                ret bytes[start:i]
            }
        }
    }
    ret nil
}

fn fmtByDefault(&arg: any): []byte {
    if arg == nil {
        ret ['<', 'n', 'i', 'l', '>']
    }

    match type arg {
    | f32:
        ret []byte(conv::FmtFloat(f64(f32(arg)), 'g', -1, 32))
    | f64:
        ret []byte(conv::FmtFloat(f64(arg), 'g', -1, 64))
    | i8:
        ret []byte(conv::FmtInt(i8(arg), 10))
    | i16:
        ret []byte(conv::FmtInt(i16(arg), 10))
    | i32:
        ret []byte(conv::FmtInt(i32(arg), 10))
    | i64:
        ret []byte(conv::FmtInt(i64(arg), 10))
    | u8:
        ret []byte(conv::FmtUint(u8(arg), 10))
    | u16:
        ret []byte(conv::FmtUint(u16(arg), 10))
    | u32:
        ret []byte(conv::FmtUint(u32(arg), 10))
    | u64:
        ret []byte(conv::FmtUint(u64(arg), 10))
    | str:
        ret []byte(str(arg))
    | bool:
        if bool(arg) {
            ret ['t', 'r', 'u', 'e']
        }
        ret ['f', 'a', 'l', 's', 'e']
    |:
        ret []byte(integrated::ToStr(arg))
    }
}

fn applyFmtByDefault(mut &j: int, &args: ...any): []byte {
    let arg = args[j]
    j++
    ret fmtByDefault(arg)
}

// Returns result of formatting.
// Parameter j is the position of argument list.
fn applyFmt(mut &fmt: []byte, mut &j: int, &args: ...any): []byte {
    // {}
    if fmt.len == 2 {
        ret applyFmtByDefault(j, args...)
    }

    // {{}} = {}
    if fmt.len == 4 && fmt == ['{', '{', '}', '}'] {
        ret ['{', '}']
    }

    ret fmt
}

fn formatBytes(fmt: str, args: ...any): []byte {
    let mut fmtBytes = []byte(fmt)

    if args.len == 0 {
        ret fmtBytes
    }

    let mut bytes = make([]byte, 0, fmt.len)

    let mut j = 0                                   // Args position.
    let mut last = 0
    let mut i = findFormatPrefix(fmtBytes, last)
    for i != -1; i = findFormatPrefix(fmtBytes, i) {
        bytes = append(bytes, fmtBytes[last:i]...)
        let mut format = getFormatRange(i, fmtBytes)
        if format == nil {
            continue
        }

        bytes = append(bytes, applyFmt(format, j, args...)...)
        if j >= args.len {
            bytes = append(bytes, fmtBytes[i:]...)
            last = fmtBytes.len
            break
        }

        // Set last positioning to latest value of i.
        last = i
    }

    if last < fmtBytes.len {
        bytes = append(bytes, fmtBytes[last:]...)
    }

    ret bytes
}

// It places the passes arguments in the string relative to the corresponding
// format string. The to_str function provided by the API is used for string
// conversion. Returns format string if args.len == 0. If the arguments
// have ended, the remaining part of format string is not processed and
// is returned as is.
//
// Formatting:
//  Arguments are processed sequentially. That is, when an argument
//  encounters a format string parameter, it will be processed according
//  to how many parameters it is. The 5th parameter uses the 5th argument
//  as the value.
//
//  Each format parameter is represented as "{}" in the format string.
//  These parameters will then be deleted according to the processing
//  algorithm and replaced with arguments.
//
//  The parameter "{{}}" is formatted as "{}" actually,
//  And does not increase argument list offset.
//
// Examples:
//  - format("{} {}!", "Hello", "World") = "Hello World!"
//  - format("{} {}") = "{} {}"
//  - format("{} is the {}", "PI Number") = "PI Number is the {}"
fn Format(fmt: str, args: ...any): str {
    ret str(formatBytes(fmt, args...))
}