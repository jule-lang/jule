// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::fs::{Status, FsError}
use path for std::fs::path

// Fileset for lexing.
struct File {
    Path:   str
    Data:   []byte
    Tokens: []&Token
}

impl File {
    // Reports whether file path is exist and accessible.
    fn IsOk(self): bool {
        _ = Status.Of(self.Path) else { ret false }
        ret true
    }

    // Fill data.
    fn Fill(mut self, mut data: []byte) {
        self.Data = data
    }

    // Returns directory of file's path.
    fn Dir(self): str {
        ret path::Dir(self.Path)
    }

    // Returns filename.
    fn Name(self): str {
        ret path::Base(self.Path)
    }

    // Returns self as uintptr.
    fn Addr(self): uintptr {
        ret uintptr(&self)
    }

    // Returns line (not include new-line char) by row.
    // Returns empty string if line is not buffer.
    fn GetRow(self, row: int): str {
        let mut n = 0
        let mut lineStart = 0
        let mut i = 0
        for i < self.Data.len; i++ {
            if self.Data[i] != '\n' {
                continue
            }
            n++
            if n == row {
                ret str(self.Data[lineStart:i])
            }
            lineStart = i + 1
        }
        ret ""
    }
}

// Returns new File points to Jule file.
fn NewFileSet(path: str): &File {
    ret &File{
        Path: path,
    }
}