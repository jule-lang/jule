// Copyright 2023 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::conv::{itoa}
use std::jule::lex::{Token, TokenId}

// Trait.
pub struct Trait {
    pub token:       Token
    pub ident:       str
    pub public:      bool
    pub mutable:     bool
    pub doc:         str
    pub methods:     []&Fn
    pub implemented: []&Struct
}

impl Kind for Trait {
    // Implement: Kind
    // Returns Trait's identifier.
    fn to_str(self): str { ret self.ident }

    // Reports whether types are same.
    fn equals(&self, other: &TypeKind): bool {
        let trt = unsafe { (*(&other)).trt() }
        if trt == nil {
            ret false
        }
        ret self == trt
    }
}

impl Trait {
    // Reports whether Trait is built-in.
    pub fn is_builtin(self): bool { ret self.token.id == TokenId.Na }

    // Returns method by identifier.
    // Returns nil if not exist any method in this identifier.
    pub fn find_method(mut self, ident: str): &Fn {
        for (_, mut f) in self.methods {
            if f.ident == ident {
                ret f
            }
        }
        ret nil
    }
}
