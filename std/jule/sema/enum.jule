// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::jule::lex::{Token}

// Enum item.
pub struct EnumItem {
    pub Token: &Token
    pub Ident: str
    pub Value: &Value
}

impl EnumItem {
    // Reports whether item has auto expression.
    pub fn AutoExpr(self): bool {
        ret self.Value == nil
    }
}

// Enum.
pub struct Enum {
    pub Token:  &Token
    pub Public: bool
    pub Ident:  str
    pub Kind:   &TypeSymbol
    pub Items:  []&EnumItem
}

impl Kind for Enum {
    // Implement: Kind
    // Returns Enum's identifier.
    pub fn Str(self): str {
        ret self.Ident
    }

    // Reports whether types are same.
    pub fn Equals(&self, other: &TypeKind): bool {
        let enm = unsafe { (*(&other)).Enum() }
        ret self == enm
    }
}

impl Enum {
    // Returns item by identifier.
    // Returns nil reference if not exist any item in this identifier.
    pub fn FindItem(mut self, ident: str): &EnumItem {
        for (_, mut item) in self.Items {
            if item.Ident == ident {
                ret item
            }
        }
        ret nil
    }
}

// TypeEnum item.
pub struct TypeEnumItem {
    pub Token: &Token
    pub Ident: str
    pub Kind:  &TypeSymbol
}

// TypeEnum.
pub struct TypeEnum {
    pub Token:  &Token
    pub Public: bool
    pub Ident:  str
    pub Items:  []&TypeEnumItem
}

impl Kind for TypeEnum {
    // Implement: Kind
    // Returns TypeEnum's identifier.
    pub fn Str(self): str {
        ret self.Ident
    }

    // Reports whether types are same.
    pub fn Equals(&self, other: &TypeKind): bool {
        let tenm = unsafe { (*(&other)).TypeEnum() }
        ret self == tenm
    }
}

impl TypeEnum {
    // Returns item by identifier.
    // Returns nil reference if not exist any item in this identifier.
    pub fn FindItem(mut self, ident: str): &TypeEnumItem {
        for (_, mut item) in self.Items {
            if item.Ident == ident {
                ret item
            }
        }
        ret nil
    }
}