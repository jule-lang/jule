// Copyright 2023 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::jule::ast::{
    FnCallExpr,
    TypeDecl,
    IdentTypeDecl,
}
use std::jule::build::{Derives, LogMsg}

// Type alias for built-in function callers.
//
// Parameters;
//  e: Caller owner Eval instance.
//  fc: Function call expression.
//  d: Data instance for evaluated expression of function.
type BuiltinCaller: fn(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data

static mut BUILTIN_FN_OUT: &FnIns = &FnIns{}
static mut BUILTIN_FN_OUTLN: &FnIns = &FnIns{}
static mut BUILTIN_FN_NEW: &FnIns = &FnIns{}
static mut BUILTIN_FN_DROP: &FnIns = &FnIns{}
static mut BUILTIN_FN_PANIC: &FnIns = &FnIns{}
static mut BUILTIN_FN_MAKE: &FnIns = &FnIns{}
static mut BUILTIN_FN_APPEND: &FnIns = &FnIns{}
static mut BUILTIN_FN_RECOVER: &FnIns = &FnIns{}
static mut BUILTIN_FN_CLONE: &FnIns = &FnIns{}
static mut BUILTIN_FN_STD_MEM_SIZE_OF: &FnIns = &FnIns{}
static mut BUILTIN_FN_STD_MEM_ALIGN_OF: &FnIns = &FnIns{}
static mut BUILTIN_FN_STD_JULE_INTEGRATED_TO_STR: &FnIns = &FnIns{}

static mut BUILTIN_FN_REAL: &FnIns = &FnIns{
    result: &TypeKind{kind: build_prim_type(str(PrimKind.Bool))},
}

static mut BUILTIN_FN_COPY: &FnIns = &FnIns{
    result: &TypeKind{kind: build_prim_type(str(PrimKind.Int))},
}

static mut BUILTIN_TYPE_ALIAS_BYTE: &TypeAlias = &TypeAlias{
    public: true,
    kind: &TypeSymbol{
        kind: &TypeKind{kind: build_prim_type(str(PrimKind.U8))},
    },
}

static mut BUILTIN_TYPE_ALIAS_RUNE: &TypeAlias = &TypeAlias{
    public: true,
    kind: &TypeSymbol{
        kind: &TypeKind{kind: build_prim_type(str(PrimKind.I32))},
    },
}

pub static mut BUILTIN_TRAIT_ERROR: &Trait = &Trait{
    public: true,
    ident:  "Error",
    methods: [
        &Fn{
            public: true,
            ident:  "error",
            params: [
                &Param{
                    ident: "self",
                },
            ],
            result: &RetType{
                kind: &TypeSymbol{
                    decl: &TypeDecl{kind: &IdentTypeDecl{ident: str(PrimKind.Str)}},
                }
            }
        },
    ],
}

// Definition of built-in Dispose trait.
pub static mut BUILTIN_TRAIT_DISPOSE: &Trait = &Trait{
    public: true,
    ident:  "Dispose",
    methods: [
        &Fn{
            public: true,
            ident:  "dispose",
            params: [
                &Param{
                    mutable: true,
                    ident:   "self",
                },
            ],
        },
    ],
}

fn init() {
    BUILTIN_FN_OUT.caller = builtin_caller_out
    BUILTIN_FN_OUTLN.caller = builtin_caller_outln
    BUILTIN_FN_NEW.caller = builtin_caller_new
    BUILTIN_FN_REAL.caller = builtin_caller_real
    BUILTIN_FN_DROP.caller = builtin_caller_drop
    BUILTIN_FN_PANIC.caller = builtin_caller_panic
    BUILTIN_FN_MAKE.caller = builtin_caller_make
    BUILTIN_FN_APPEND.caller = builtin_caller_append
    BUILTIN_FN_COPY.caller = builtin_caller_copy
    BUILTIN_FN_RECOVER.caller = builtin_caller_recover
    BUILTIN_FN_CLONE.caller = builtin_caller_clone

    BUILTIN_FN_STD_MEM_SIZE_OF.caller = builtin_caller_std_mem_size_of
    BUILTIN_FN_STD_MEM_ALIGN_OF.caller = builtin_caller_std_mem_align_of

    BUILTIN_FN_STD_JULE_INTEGRATED_TO_STR.caller = builtin_caller_std_jule_integrated_to_str

    let mut error_method = BUILTIN_TRAIT_ERROR.find_method("error")
    let mut error_method_ins = error_method.instance()
    error_method_ins.result = &TypeKind{kind: build_prim_type(str(PrimKind.Str))}
    error_method_ins.caller = builtin_caller_common
    error_method.append_instance(error_method_ins)

    let mut dispose_method = BUILTIN_TRAIT_DISPOSE.find_method("dispose")
    let mut dispose_method_ins = dispose_method.instance()
    dispose_method_ins.caller = builtin_caller_common
    dispose_method.append_instance(dispose_method_ins)
}

fn find_builtin_fn(ident: str): &FnIns {
    match ident {
    | "out":     ret BUILTIN_FN_OUT
    | "outln":   ret BUILTIN_FN_OUTLN
    | "new":     ret BUILTIN_FN_NEW
    | "real":    ret BUILTIN_FN_REAL
    | "drop":    ret BUILTIN_FN_DROP
    | "panic":   ret BUILTIN_FN_PANIC
    | "make":    ret BUILTIN_FN_MAKE
    | "append":  ret BUILTIN_FN_APPEND
    | "copy":    ret BUILTIN_FN_COPY
    | "recover": ret BUILTIN_FN_RECOVER
    | "clone":   ret BUILTIN_FN_CLONE
    |:           ret new(FnIns)
    }
}

fn find_builtin_type_alias(ident: str): &TypeAlias {
    match ident {
    | "byte": ret BUILTIN_TYPE_ALIAS_BYTE
    | "rune": ret BUILTIN_TYPE_ALIAS_RUNE
    |:        ret new(TypeAlias)
    }
}

fn find_builtin_trait(ident: str): &Trait {
    match ident {
    | "Error":   ret BUILTIN_TRAIT_ERROR
    | "Dispose": ret BUILTIN_TRAIT_DISPOSE
    |:           ret new(Trait)
    }
}

fn find_builtin_def(ident: str): any {
    let mut f = find_builtin_fn(ident)
    if real(f) {
        ret f
    }

    let mut ta = find_builtin_type_alias(ident)
    if real(ta) {
        ret ta
    }

    let mut t = find_builtin_trait(ident)
    if real(t) {
        ret t
    }

    ret nil
}

fn find_builtin_def_std_mem(ident: str): any {
    match ident {
    | "size_of":  ret BUILTIN_FN_STD_MEM_SIZE_OF
    | "align_of": ret BUILTIN_FN_STD_MEM_ALIGN_OF
    |:            ret nil
    }
}

fn find_builtin_def_std_jule_integrated(ident: str): any {
    match ident {
    | "to_str":  ret BUILTIN_FN_STD_JULE_INTEGRATED_TO_STR
    |:           ret nil
    }
}

fn find_package_builtin_def(link_path: str, ident: str): any {
    match link_path {
    | "std::mem":              ret find_builtin_def_std_mem(ident)
    | "std::jule::integrated": ret find_builtin_def_std_jule_integrated(ident)
    |:                         ret nil
    }
}

fn builtin_caller_common_plain(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    let mut f = d.kind.fnc()

    let mut fcac = FnCallArgChecker{
        e:                  e,
        f:                  f,
        args:               fc.args,
        dynamic_annotation: false,
        error_token:        fc.token,
    }
    _ = fcac.check()

    let mut model = &FnCallExprModel{
        func:  f,
        is_co: fc.concurrent,
        expr:  d.model,
        args:  fcac.arg_models,
    }

    if !real(f.result) {
        d = build_void_data()
    } else {
        d = &Data{
            kind: f.result,
        }
    }

    d.model = model
    ret d
}

fn builtin_caller_common(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }

    ret builtin_caller_common_plain(e, fc, d)
}

fn builtin_caller_common_mut(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    if !d.mutable {
        e.push_err(fc.token, LogMsg.MutOperationOnImmut)
    }
    ret builtin_caller_common(e, fc, d)
}

fn builtin_caller_out(mut &e: &Eval, mut &fc: &FnCallExpr, mut &_: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "v")
        ret new(Data)
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[1].token, LogMsg.ArgumentOverflow)
    }

    let mut expr = e.eval_expr(fc.args[0])
    if !real(expr) {
        ret new(Data)
    }

    if expr.is_void() || real(expr.kind.fnc()) || real(expr.kind.tup()) {
        e.push_err(fc.args[0].token, LogMsg.InvalidExpr)
        ret new(Data)
    }

    let mut d = build_void_data()
    d.model = &BuiltinOutCallExprModel{expr: expr.model}
    ret d
}

fn builtin_caller_outln(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    d = builtin_caller_out(e, fc, d)
    if !real(d) {
        ret new(Data)
    }

    d.model = &BuiltinOutlnCallExprModel{
        expr: (&BuiltinOutCallExprModel)(d.model).expr,
    }
    ret d
}

fn builtin_caller_new(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "type")
        ret new(Data)
    }
    if fc.args.len > 2 {
        e.push_err(fc.args[2].token, LogMsg.ArgumentOverflow)
    }

    let mut t = e.eval_expr_kind(fc.args[0].kind)
    if !real(t) {
        ret new(Data)
    }

    if !t.decl {
        e.push_err(fc.args[0].token, LogMsg.InvalidType)
        ret new(Data)
    }

    if !is_valid_for_ref_type(t.kind) {
        let mut s = t.kind.strct()
        if real(s) && real(s.decl) && s.decl.cpp_linked {
            e.push_err(fc.args[0].token, LogMsg.CppLinkedStructForRef)
        } else {
            e.push_err(fc.args[0].token, LogMsg.InvalidType)
        }
        ret new(Data)
    }

    d.kind = &TypeKind{kind: &Ref{elem: t.kind.clone()}}

    if fc.args.len == 2 { // Initialize expression.
        let mut old = e.prefix
        e.prefix = t.kind

        let mut init = e.eval_expr(fc.args[1])

        e.prefix = old

        if real(init) {
            let mut token = fc.args[1].token
            const REFERENCE = false
            e.s.check_validity_for_init_expr(
                !e.immutable, REFERENCE, t.kind, init, token)
            e.s.check_assign_type(false, t.kind, init, token)

            d.model = &BuiltinNewCallExprModel{
                kind: t.kind,
                init: init.model,
            }
        }
    } else {
        d.model = &BuiltinNewCallExprModel{kind: t.kind}
    }

    ret d
}

fn builtin_caller_real(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "ref")
        ret new(Data)
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[1].token, LogMsg.ArgumentOverflow)
    }

    let mut ref = e.eval_expr(fc.args[0])
    if !real(ref) {
        ret new(Data)
    }

    if !real(ref.kind.ref()) {
        e.push_err(fc.args[0].token, LogMsg.InvalidExpr)
        e.push_suggestion(LogMsg.ExpressionMustBeReferenceType)
        ret new(Data)
    }

    d.kind = BUILTIN_FN_REAL.result
    d.model = &BuiltinRealCallExprModel{expr: ref.model}
    ret d
}

fn builtin_caller_drop(mut &e: &Eval, mut &fc: &FnCallExpr, mut &_: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "ref")
        ret new(Data)
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[2].token, LogMsg.ArgumentOverflow)
    }

    let mut ref = e.eval_expr(fc.args[0])
    if !real(ref) {
        ret new(Data)
    }

    if !real(ref.kind.ref()) {
        e.push_err(fc.args[0].token, LogMsg.InvalidExpr)
        e.push_suggestion(LogMsg.ExpressionMustBeReferenceType)
        ret new(Data)
    }

    _ = check_mut(e.s, ref, new(Data), fc.args[0].token)

    let mut d = build_void_data()
    d.model = &BuiltinDropCallExprModel{expr: ref.model}
    ret d
}

fn builtin_caller_panic(mut &e: &Eval, mut &fc: &FnCallExpr, mut &_: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "error")
        ret new(Data)
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[2].token, LogMsg.ArgumentOverflow)
    }

    let mut expr = e.eval_expr(fc.args[0])
    if !real(expr) {
        ret new(Data)
    }

    if real(expr.kind.strct()) {
        let s = expr.kind.strct()
        if s.decl.is_implements(BUILTIN_TRAIT_ERROR) {
            expr.cast_kind = &TypeKind{
                kind: BUILTIN_TRAIT_ERROR,
            }
            apply_cast_kind(expr)
        }
    }

    let mut d = build_void_data()
    d.model = &BuiltinPanicCallExprModel{expr: expr.model}
    ret d
}

fn builtin_caller_make(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    if fc.args.len < 2 {
        if fc.args.len == 1 {
            e.push_err(fc.token, LogMsg.MissingExprFor, "size")
            ret new(Data)
        }
        e.push_err(fc.token, LogMsg.MissingExprFor, "type, size")
        ret new(Data)
    }
    if fc.args.len > 3 {
        e.push_err(fc.args[3].token, LogMsg.ArgumentOverflow)
    }

    let mut t = e.eval_expr_kind(fc.args[0].kind)
    if !real(t) {
        ret new(Data)
    }

    if !t.decl || !real(t.kind.slc()) {
        e.push_err(fc.args[0].token, LogMsg.InvalidType)
        ret new(Data)
    }

    d.kind = t.kind

    let mut len = e.s.evalp(e.lookup, t.kind).eval_expr(fc.args[1])
    if !real(len) {
        ret d
    }

    e.check_integer_indexing_by_data(len, fc.args[1].token)

    if fc.args.len == 3 {
        let mut cap = e.s.evalp(e.lookup, t.kind).eval_expr(fc.args[2])
        if !real(cap) {
            ret d
        }

        e.check_integer_indexing_by_data(cap, fc.args[2].token)
        d.model = &BuiltinMakeCallExprModel{
            kind: t.kind,
            len:  len.model,
            cap:  cap.model,
        }
    } else {
        d.model = &BuiltinMakeCallExprModel{
            kind: t.kind,
            len: len.model,
        }
    }

    ret d
}

fn builtin_caller_append(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    if fc.args.len < 2 {
        if fc.args.len == 1 {
            e.push_err(fc.token, LogMsg.MissingExprFor, "src")
            ret new(Data)
        }
        e.push_err(fc.token, LogMsg.MissingExprFor, "src, values")
        ret new(Data)
    }

    let mut t = e.eval_expr(fc.args[0])
    if !real(t) {
        ret new(Data)
    }

    if !real(t.kind.slc()) {
        e.push_err(fc.args[0].token, LogMsg.InvalidExpr)
        ret new(Data)
    }

    let mut f = &FnIns{
        params: [
            &ParamIns{
                decl: &Param{
                    mutable: true,
                },
                kind: t.kind.clone(),
            },
            &ParamIns{
                decl: &Param{
                    mutable:  true,
                    variadic: true,
                },
                kind: t.kind.slc().elem.clone(),
            },
        ],
        result: t.kind.clone(),
        caller: builtin_caller_common_plain,
    }
    d.kind = &TypeKind{kind: f}
    d.model = &CommonIdentExprModel{ident: "append"}

    d = builtin_caller_common_plain(e, fc, d)
    if real(d) {
        let mut model = (&FnCallExprModel)(d.model)
        d.model = &BuiltinAppendCallExprModel{
            dest:     model.args[0],
            elements: model.args[1],
        }
    }
    ret d
}

fn builtin_caller_copy(mut &e: &Eval, mut &fc: &FnCallExpr, mut &d: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    if fc.args.len < 2 {
        if fc.args.len == 1 {
            e.push_err(fc.token, LogMsg.MissingExprFor, "src")
            ret new(Data)
        }
        e.push_err(fc.token, LogMsg.MissingExprFor, "src, values")
        ret new(Data)
    }
    if fc.args.len > 2 {
        e.push_err(fc.args[2].token, LogMsg.ArgumentOverflow)
    }

    let mut t = e.eval_expr(fc.args[0])
    if !real(t) {
        ret new(Data)
    }

    if !real(t.kind.slc()) {
        e.push_err(fc.args[0].token, LogMsg.InvalidExpr)
        ret new(Data)
    }

    if !t.mutable {
        e.push_err(fc.args[0].token, LogMsg.MutOperationOnImmut)
    }

    let mut f = &FnIns{
        params: [
            &ParamIns{
                decl: &Param{},
                kind: t.kind.clone(),
            },
            &ParamIns{
                decl: &Param{
                    mutable: true,
                },
                kind: t.kind.clone(),
            },
        ],
        result: BUILTIN_FN_COPY.result,
        caller: builtin_caller_common_plain,
    }

    d.kind = &TypeKind{kind: f}
    d.model = &CommonIdentExprModel{ident: "copy"}

    d = builtin_caller_common_plain(e, fc, d)
    ret d
}

fn builtin_caller_recover(mut &e: &Eval, mut &fc: &FnCallExpr, mut &_: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    let HANDLER_KIND = &TypeKind{
        kind: &FnIns{
            decl: &Fn{},
            params: [
                &ParamIns{
                    decl: &Param{
                        ident: "err",
                    },
                    kind: &TypeKind{
                        kind: BUILTIN_TRAIT_ERROR,
                    },
                },
            ],
        },
    }

    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "handler")
        ret new(Data)
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[1].token, LogMsg.ArgumentOverflow)
    }

    let mut t = e.eval_expr(fc.args[0])
    if !real(t) {
        ret new(Data)
    }

    let f = t.kind.fnc()
    if !real(f) {
        e.push_err(fc.args[0].token, LogMsg.InvalidExpr)
        ret new(Data)
    }

    if !f.equals(HANDLER_KIND) {
        e.push_err(fc.args[0].token, LogMsg.IncompatibleTypes, t.kind.to_str(), HANDLER_KIND.to_str())
    }

    let mut d = build_void_data()
    d.kind = t.kind.clone()
    d.model = &Recover{
        handler_expr: t.model,
    }
    ret d
}

fn builtin_caller_clone(mut &e: &Eval, mut &fc: &FnCallExpr, mut &_: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }

    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "expr")
        ret new(Data)
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[1].token, LogMsg.ArgumentOverflow)
    }

    let mut d = e.eval_expr_kind(fc.args[0].kind)
    if !real(d) {
        ret new(Data)
    }

    let check_kind = fn(mut k: &TypeKind) {
        match {
        | real(k.slc()):
            if !k.slc().elem.supports_cloning() {
                e.push_err(fc.args[0].token, LogMsg.InternalTypeNotSupportsClone, k.to_str())
            }

        | real(k.map()):
            let m = k.map()
            if !m.key.supports_cloning() || !m.val.supports_cloning() {
                e.push_err(fc.args[0].token, LogMsg.InternalTypeNotSupportsClone, k.to_str())
            }

        | real(k.arr()):
            if !k.arr().elem.supports_cloning() {
                e.push_err(fc.args[0].token, LogMsg.InternalTypeNotSupportsClone, k.to_str())
            }

        | real(k.ref()):
            if !k.ref().elem.supports_cloning() {
                e.push_err(fc.args[0].token, LogMsg.InternalTypeNotSupportsClone, k.to_str())
            }

        | real(k.strct()):
            let s = d.kind.strct()
            if !s.mutable {
                e.push_err(fc.args[0].token, LogMsg.CloneImmutStruct, d.kind.to_str())
            }
            if !real(s.decl) || s.decl.cpp_linked || !s.decl.is_derives(str(Derives.Clone)) {
                e.push_err(fc.args[0].token, LogMsg.TypeIsNotDerives, d.kind.to_str(), str(Derives.Clone))
            }

        |:
            e.push_err(fc.args[0].token, LogMsg.TypeIsNotDerives, d.kind.to_str(), str(Derives.Clone))
        }
    }

    match {
    | !d.lvalue:
        e.push_err(fc.args[0].token, LogMsg.CloneNonLvalue)

    | !d.kind.supports_cloning():
        check_kind(d.kind)
    }

    d.mutable = true
    d.model = &BuiltinCloneCallExprModel{expr: d.model}
    ret d
}

fn builtin_caller_std_mem_size_of(mut &e: &Eval, mut &fc: &FnCallExpr, mut &_: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    let mut result = &Data{
        kind: &TypeKind{kind: build_prim_type(str(PrimKind.Uint))},
    }

    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "type|expr")
        ret result
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[1].token, LogMsg.ArgumentOverflow)
    }

    let mut d = e.eval_expr_kind(fc.args[0].kind)
    if !real(d) {
        ret result
    }

    result.model = &SizeofExprModel{expr: d.model}
    ret result
}

fn builtin_caller_std_mem_align_of(mut &e: &Eval, mut &fc: &FnCallExpr, mut &_: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }
    let mut result = &Data{
        kind: &TypeKind{kind: build_prim_type(str(PrimKind.Uint))},
    }

    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "type|expr")
        ret result
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[1].token, LogMsg.ArgumentOverflow)
    }

    let mut d = e.eval_expr_kind(fc.args[0].kind)
    if !real(d) {
        ret result
    }

    result.model = &AlignofExprModel{expr: d.model}
    ret result
}

fn builtin_caller_std_jule_integrated_to_str(mut &e: &Eval, mut &fc: &FnCallExpr, mut &_: &Data): &Data {
    if fc.generics.len > 0 {
        e.push_err(fc.token, LogMsg.NotHasGenerics)
    }

    if fc.args.len < 1 {
        e.push_err(fc.token, LogMsg.MissingExprFor, "expr")
        ret new(Data)
    }
    if fc.args.len > 1 {
        e.push_err(fc.args[1].token, LogMsg.ArgumentOverflow)
    }

    let mut d = e.eval_expr_kind(fc.args[0].kind)
    if !real(d) {
        ret new(Data)
    }

    ret &Data{
        kind:  &TypeKind{kind: build_prim_type(str(PrimKind.Str))},
        model: &IntegratedToStrExprModel{
            expr: d.model,
        },
    }
}
