// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/fmt"
use "std/jule/ast"
use "std/jule/build"
use "std/jule/constant"
use "std/jule/internal/mod"
use "std/jule/types"

// Type alias for built-in function callers.
//
// Parameters;
//  e: Caller owner Eval instance.
//  fc: Function call expression.
//  d: Data instance for evaluated expression of function.
type builtinCaller: fn(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data

fn findBuiltinFunc(&ident: str): &FnIns {
	match ident {
	| "print":
		static mut f = &FnIns{caller: builtinCallerPrint}
		ret f
	| "println":
		static mut f = &FnIns{caller: builtinCallerPrintln}
		ret f
	| "new":
		static mut f = &FnIns{caller: builtinCallerNew}
		ret f
	| "panic":
		static mut f = &FnIns{caller: builtinCallerPanic}
		ret f
	| "make":
		static mut f = &FnIns{caller: builtinCallerMake}
		ret f
	| "append":
		static mut f = &FnIns{caller: builtinCallerAppend}
		ret f
	| "copy":
		static mut f = &FnIns{caller: builtinCallerCopy}
		ret f
	| "len":
		static mut f = &FnIns{caller: builtinCallerLen}
		ret f
	| "cap":
		static mut f = &FnIns{caller: builtinCallerCap}
		ret f
	| "delete":
		static mut f = &FnIns{caller: builtinCallerDelete}
		ret f
	|:
		ret nil
	}
}

fn primTypeAlias(mut &k: &Type): &TypeAlias {
	ret &TypeAlias{
		Public: true,
		TypeSym: &TypeSym{
			Type: k,
		},
	}
}

static mut primNil = &Type{Kind: nil}
static mut primVoid = &Type{Kind: buildPrimType("void")}
static mut primAny = &Type{Kind: buildPrimType(types::Kind.Any)}
static mut primStr = &Type{Kind: buildPrimType(types::Kind.Str)}
static mut primBool = &Type{Kind: buildPrimType(types::Kind.Bool)}
static mut primUintptr = &Type{Kind: buildPrimType(types::Kind.Uintptr)}
static mut primUint = &Type{Kind: buildPrimType(types::Kind.Uint)}
static mut primInt = &Type{Kind: buildPrimType(types::Kind.Int)}
static mut primI8 = &Type{Kind: buildPrimType(types::Kind.I8)}
static mut primI16 = &Type{Kind: buildPrimType(types::Kind.I16)}
static mut primI32 = &Type{Kind: buildPrimType(types::Kind.I32)}
static mut primI64 = &Type{Kind: buildPrimType(types::Kind.I64)}
static mut primU8 = &Type{Kind: buildPrimType(types::Kind.U8)}
static mut primU16 = &Type{Kind: buildPrimType(types::Kind.U16)}
static mut primU32 = &Type{Kind: buildPrimType(types::Kind.U32)}
static mut primU64 = &Type{Kind: buildPrimType(types::Kind.U64)}
static mut primF32 = &Type{Kind: buildPrimType(types::Kind.F32)}
static mut primF64 = &Type{Kind: buildPrimType(types::Kind.F64)}

fn findBuiltinVar(&ident: str): &Var {
	match ident {
	| "nil":
		static mut v = &Var{
			Public: true,
			Mutable: false,
			TypeSym: &TypeSym{
				Type: primNil,
			},
			Constant: true,
			Value: &Value{
				Data: &Data{
					Constant: constant::Const.NewNil(),
				},
			},
		}
		ret v
	| "true":
		static mut v = &Var{
			Public: true,
			Mutable: false,
			TypeSym: findBuiltinTypeAlias("bool").TypeSym,
			Constant: true,
			Value: &Value{
				Data: &Data{
					Constant: constant::Const.NewBool(true),
				},
			},
		}
		ret v
	| "false":
		static mut v = &Var{
			Public: true,
			Mutable: false,
			TypeSym: findBuiltinTypeAlias("bool").TypeSym,
			Constant: true,
			Value: &Value{
				Data: &Data{
					Constant: constant::Const.NewBool(false),
				},
			},
		}
		ret v
	|:
		ret nil
	}
}

fn findBuiltinTypeAlias(ident: str): &TypeAlias {
	match ident {
	| "any":
		static mut t = primTypeAlias(primAny)
		ret t
	| "str":
		static mut t = primTypeAlias(primStr)
		ret t
	| "bool":
		static mut t = primTypeAlias(primBool)
		ret t
	| "uintptr":
		static mut t = primTypeAlias(primUintptr)
		ret t
	| "uint":
		static mut t = primTypeAlias(primUint)
		ret t
	| "int":
		static mut t = primTypeAlias(primInt)
		ret t
	| "i8":
		static mut t = primTypeAlias(primI8)
		ret t
	| "i16":
		static mut t = primTypeAlias(primI16)
		ret t
	| "i64":
		static mut t = primTypeAlias(primI64)
		ret t
	| "u16":
		static mut t = primTypeAlias(primU16)
		ret t
	| "u32":
		static mut t = primTypeAlias(primU32)
		ret t
	| "u64":
		static mut t = primTypeAlias(primU64)
		ret t
	| "f32":
		static mut t = primTypeAlias(primF32)
		ret t
	| "f64":
		static mut t = primTypeAlias(primF64)
		ret t
	| "byte" | "u8":
		static mut t = primTypeAlias(primU8)
		ret t
	| "rune" | "i32":
		static mut t = primTypeAlias(primI32)
		ret t
	|:
		ret nil
	}
}

fn findBuiltinDef(&ident: str): any {
	mut v := findBuiltinVar(ident)
	if v != nil {
		ret v
	}
	mut f := findBuiltinFunc(ident)
	if f != nil {
		ret f
	}
	mut ta := findBuiltinTypeAlias(ident)
	if ta != nil {
		ret ta
	}
	ret nil
}

fn findBuiltinDefStdMem(&ident: str): any {
	match ident {
	| "SizeOf":
		static mut f = &FnIns{caller: builtinCallerStdMemSizeOf}
		ret f
	| "AlignOf":
		static mut f = &FnIns{caller: builtinCallerStdMemAlignOf}
		ret f
	| "Free":
		static mut f = &FnIns{caller: builtinCallerStdMemFree}
		ret f
	|:
		ret nil
	}
}

fn findBuiltinDefStdComptime(&ident: str): any {
	match ident {
	| "TypeOf":
		static mut f = &FnIns{caller: builtinCallerStdComptimeTypeOf}
		ret f
	| "ValueOf":
		static mut f = &FnIns{caller: builtinCallerStdComptimeValueOf}
		ret f
	| "Line":
		static mut f = &FnIns{caller: builtinCallerStdComptimeLine}
		ret f
	| "File":
		static mut f = &FnIns{caller: builtinCallerStdComptimeFile}
		ret f
	| "Files":
		static mut f = &FnIns{caller: builtinCallerStdComptimeFiles}
		ret f
	| "TypeAlias":
		static mut f = &FnIns{caller: builtinCallerStdComptimeTypeAlias}
		ret f
	|:
		ret nil
	}
}

fn findBuiltinDefStdJuleIntegrated(&ident: str): any {
	match ident {
	| "Emit":
		static mut f = &FnIns{
			Decl: &Fn{
				Generics: make([]&ast::GenericDecl, 1),
			},
			caller: builtinCallerStdJuleIntegratedEmit,
		}
		ret f
	|:
		ret nil
	}
}

fn findPackageBuiltinDef(&linkPath: str, &ident: str): any {
	match linkPath {
	| "std/comptime":
		ret findBuiltinDefStdComptime(ident)
	| "std/mem":
		ret findBuiltinDefStdMem(ident)
	| "std/jule/integrated":
		ret findBuiltinDefStdJuleIntegrated(ident)
	|:
		ret nil
	}
}

fn builtinCallerError(mut &e: &eval, mut &fc: &ast::FnCallExpr): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "err")
		ret nil
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "error")
	}
	mut expr := e.evalExpr(fc.Args[0])
	if expr == nil {
		ret nil
	}

	mut token := fc.Args[0].Token
	if expr.IsNil() {
		e.pushErr(token, build::LogMsg.NilError)
	}

	const Reference = false
	const Mutable = true
	if !e.s.checkValidityForInitExpr(Mutable, Reference, primAny, expr, token) {
		ret nil
	}

	applyRuntimeToStr(e.s, expr.Type, primAny, token, e.getOwnerRefers())

	mut d := buildVoidData()
	d.Model = &BuiltinErrorCallExprModel{Err: expr}
	ret d
}

fn builtinCallerPrint(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "v")
		ret nil
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "print")
	}

	mut arg := fc.Args[0]
	mut expr := e.evalExpr(arg)
	if expr == nil {
		ret nil
	}

	if !isBuiltinStrConvertable(expr.Type) {
		e.pushErr(arg.Token, build::LogMsg.InvalidExpr)
		ret nil
	}

	pushRuntimeToStr(e.s, expr.Type, arg.Token, e.getOwnerRefers())

	mut d := buildVoidData()
	d.Model = &BuiltinPrintCallExprModel{Expr: expr}
	ret d
}

fn builtinCallerPrintln(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	d = builtinCallerPrint(e, fc, d)
	if d == nil {
		ret nil
	}

	d.Model = &BuiltinPrintlnCallExprModel{
		Expr: (&BuiltinPrintCallExprModel)(d.Model).Expr,
	}
	ret d
}

fn builtinCallerNew(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "type")
		ret nil
	}
	if len(fc.Args) > 2 {
		e.pushErr(fc.Args[2].Token, build::LogMsg.ArgumentOverflow, "new")
	}

	mut t := e.eval1(fc.Args[0])
	if t == nil {
		ret nil
	}

	if !t.Decl {
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidType)
		ret nil
	}

	if !isValidForSptrType(t.Type) {
		mut s := t.Type.Struct()
		if s != nil && s.Decl != nil && s.Decl.Binded {
			e.pushErr(fc.Args[0].Token, build::LogMsg.BindedStructForRef)
		}
		ret nil
	}

	d.Type = &Type{
		Kind: &Sptr{
			Elem: t.Type,
		},
	}

	if len(fc.Args) == 2 { // Initialize expression.
		mut old := e.prefix
		e.prefix = t.Type

		mut init := e.evalExpr(fc.Args[1])

		e.prefix = old

		if init != nil {
			mut token := fc.Args[1].Token
			const Reference = false
			e.s.checkValidityForInitExpr(
				!e.immutable, Reference, t.Type, init, token)
			_ = e.s.checkAssignType(false, t.Type, init, token, e.getOwnerRefers())

			d.Model = &BuiltinNewCallExprModel{
				Kind: t.Type,
				Init: init.Model,
			}
		}
	} else {
		d.Model = &BuiltinNewCallExprModel{
			Kind: t.Type,
		}
	}

	ret d
}

fn builtinCallerPanic(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "message")
		ret nil
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[2].Token, build::LogMsg.ArgumentOverflow, "panic")
	}

	mut expr := e.evalExpr(fc.Args[0])
	if expr == nil {
		ret nil
	}

	if expr.Type.Prim() == nil || !expr.Type.Prim().IsStr() {
		e.pushErr(fc.Token, build::LogMsg.PanicedWithNonStr)
	}

	mut d := buildVoidData()
	d.Model = &BuiltinPanicCallExprModel{
		Token: fc.Token,
		Expr: expr.Model,
	}
	ret d
}

fn builtinCallerMake(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if len(fc.Args) < 2 {
		if len(fc.Args) == 1 {
			e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "size")
			ret nil
		}
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "type, size")
		ret nil
	} else if len(fc.Args) > 3 {
		e.pushErr(fc.Args[3].Token, build::LogMsg.ArgumentOverflow, "make")
		ret nil
	}

	mut t := e.evalExprKind(fc.Args[0].Kind)
	if t == nil {
		ret nil
	}

	if !t.Decl {
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidType)
		ret nil
	} else if t.Type.Slc() == nil {
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidTypeForFn, t.Type.Str(), "make")
		ret nil
	}

	d.Type = t.Type

	mut lenExpr := e.s.evalp(e.lookup, t.Type).evalExpr(fc.Args[1])
	if lenExpr == nil {
		ret nil
	}

	e.checkIntegerIndexingByData(lenExpr, fc.Args[1].Token)

	mut model := &BuiltinMakeCallExprModel{
		Kind: t.Type,
		Len: lenExpr.Model,
	}
	d.Model = model
	if len(fc.Args) == 3 {
		mut capExpr := e.s.evalp(e.lookup, t.Type).evalExpr(fc.Args[2])
		if capExpr == nil {
			ret d
		}
		e.checkIntegerIndexingByData(capExpr, fc.Args[2].Token)
		model.Cap = capExpr.Model
	}
	ret d
}

fn builtinCallerAppend(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if len(fc.Args) < 2 {
		if len(fc.Args) == 1 {
			e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "values")
			ret nil
		}
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "src, values")
		ret nil
	}

	mut firstArg := fc.Args[0]
	mut t := e.evalExpr(firstArg)
	if t == nil {
		ret nil
	}

	mut s := t.Type.Slc()
	if s == nil {
		e.pushErr(firstArg.Token, build::LogMsg.InvalidExpr)
		ret nil
	}

	const reference = false // Element parameter is not reference.
	const mutable = true    // Element parameter is mutable.

	if e.s.checkValidityForInitExpr(mutable, reference, t.Type, t, firstArg.Token) {
		// Check type if validity is good.
		// Helps to reduce error logs and duplicated logs.
		_ = e.s.checkAssignType(reference, t.Type, t, firstArg.Token, e.getOwnerRefers())
	}

	mut prefix := e.prefix
	e.prefix = t.Type
	defer { e.prefix = prefix }

	d = &Data{
		Mutable: true,
		Type: t.Type,
	}
	mut secondArg := fc.Args[1]
	match type secondArg.Kind {
	| &ast::VariadicExpr:
		if len(fc.Args) > 2 {
			e.pushErr(fc.Args[2].Token, build::LogMsg.MoreArgsWithVariadiced)
		}

		mut e1 := e.evalExpr((&ast::VariadicExpr)(secondArg.Kind).Expr)
		if e1 == nil {
			ret nil
		}
		if s.Elem.Prim() != nil && s.Elem.Prim().IsU8() { // []byte
			if e1.Type.Prim() != nil && e1.Type.Prim().IsStr() {
				// append([]byte, str...)
				makeVariadic(e1, primU8)
				d.Model = &BuiltinAppendCallExprModel{
					Dest: t.Model,
					Elements: e1.Model,
				}
				ret d
			}
		}

		if !e1.Type.Variadicable() {
			e.pushErr(secondArg.Token, build::LogMsg.VariadicWithNonVariadicable, e1.Type.Str())
			ret nil
		}
		makeVariadic(e1, s.Elem)

		// Before the analysis, set variadic kind as not variadic.
		// This is should be made to avoid mutability error for base type of slice.
		// Element types should still be checked.
		// Since the variadiced slice will not mutated, just check mutability risk of elements.
		e1.Type.Variadic = false

		if e.s.checkValidityForInitExpr(mutable, reference, s.Elem, e1, secondArg.Token) {
			e1.Type.Variadic = true
			// Check type if validity is good.
			// Helps to reduce error logs and duplicated logs.
			_ = e.s.checkAssignType(reference, s.Elem, e1, secondArg.Token, e.getOwnerRefers())
		} else {
			e1.Type.Variadic = true
		}
		d.Model = &BuiltinAppendCallExprModel{
			Dest: t.Model,
			Elements: e1.Model,
		}
		ret d
	}

	mut e1 := e.evalExpr(secondArg)
	if e1 == nil {
		ret nil
	}

	mut slice := &SliceExprModel{
		ElemKind: s.Elem,
		Elems: make([]ExprModel, 0, len(fc.Args)-1),
	}
	for (_, mut arg) in fc.Args[1:] {
		e1 = e.evalExpr(arg)
		if e1 == nil {
			ret nil
		}
		if e1.Type.Variadic {
			e.pushErr(arg.Token, build::LogMsg.MoreArgsWithVariadiced)
		}
		if e.s.checkValidityForInitExpr(mutable, reference, s.Elem, e1, arg.Token) {
			// Check type if validity is good.
			// Helps to reduce error logs and duplicated logs.
			_ = e.s.checkAssignType(reference, s.Elem, e1, arg.Token, e.getOwnerRefers())
		}
		slice.Elems = append(slice.Elems, e1.Model)
	}
	d.Model = &BuiltinAppendCallExprModel{
		Dest: t.Model,
		Elements: slice,
	}
	ret d
}

fn callerCopySlice(mut &dest: &Data, mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if !dest.Mutable {
		e.pushErr(fc.Args[0].Token, build::LogMsg.MutOperationOnImmut)
	}

	mut src := e.evalExpr(fc.Args[1])
	if src == nil {
		ret nil
	}
	mut slc := src.Type.Slc()

	if slc != nil {
		if !src.Mutable && slc.Elem.Mutable() {
			e.pushErr(fc.Args[1].Token, build::LogMsg.MutOperationOnImmut)
		}
		if !dest.Type.Equal(src.Type) {
			e.pushErr(fc.Args[1].Token, build::LogMsg.IncompatibleTypes, dest.Type.Str(), src.Type.Str())
		}
	} else {
		destPrim := dest.Type.Slc().Elem.Prim()
		prim := src.Type.Prim()
		if destPrim == nil || prim == nil || !prim.IsStr() || !destPrim.IsU8() {
			e.pushErr(fc.Args[1].Token, build::LogMsg.IncompatibleTypes, dest.Type.Str(), src.Type.Str())
		}
	}

	d.Type = lenKind()
	d.Model = &BuiltinCopyCallExprModel{
		Dest: dest,
		Src: src,
	}
	ret d
}

fn builtinCallerCopy(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if len(fc.Args) < 2 {
		if len(fc.Args) == 1 {
			e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "src")
			ret nil
		}
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "src, values")
		ret nil
	}
	if len(fc.Args) > 2 {
		e.pushErr(fc.Args[2].Token, build::LogMsg.ArgumentOverflow, "copy")
	}

	mut dest := e.evalExpr(fc.Args[0])
	match {
	| dest == nil:
		ret nil
	| dest.Type.Slc() != nil:
		ret callerCopySlice(dest, e, fc, d)
	|:
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidType)
		ret nil
	}
}

fn callerLenSlice(mut &expr: &Data, mut &d: &Data): &Data {
	d.Type = lenKind()
	d.Model = &BuiltinLenCallExprModel{
		Expr: expr,
	}
	ret d
}

fn callerLenMap(mut &expr: &Data, mut &d: &Data): &Data {
	d.Type = lenKind()
	d.Model = &BuiltinLenCallExprModel{
		Expr: expr,
	}
	ret d
}

fn callerLenArr(mut &expr: &Data, mut &d: &Data): &Data {
	d.Type = lenKind()
	d.Constant = constant::Const.NewI64(i64(expr.Type.Arr().N))
	d.Model = d.Constant
	ret d
}

fn callerLenStr(mut &expr: &Data, mut &d: &Data): &Data {
	d.Type = lenKind()
	if expr.IsConst() {
		d.Constant = constant::Const.NewI64(i64(len(expr.Constant.ReadStr())))
		d.Model = d.Constant
	} else {
		d.Model = &BuiltinLenCallExprModel{
			Expr: expr,
		}
	}
	ret d
}

fn callerLenComptime(mut &d: &Data, n: int): &Data {
	d.Type = lenKind()
	d.Constant = constant::Const.NewI64(i64(n))
	d.Model = d.Constant
	ret d
}

fn builtinCallerLen(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "obj")
		ret nil
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "len")
	}
	mut dest := e.evalExpr1(fc.Args[0])
	match {
	| dest == nil:
		ret nil
	| dest.Type.Slc() != nil:
		ret callerLenSlice(dest, d)
	| dest.Type.Arr() != nil:
		ret callerLenArr(dest, d)
	| dest.Type.Map() != nil:
		ret callerLenMap(dest, d)
	| dest.Type.Prim() != nil && dest.Type.Prim().IsStr():
		ret callerLenStr(dest, d)
	| dest.Type.comptimeStructFields() != nil:
		ret callerLenComptime(d, len(dest.Type.comptimeStructFields().fields))
	| dest.Type.comptimeEnumFields() != nil:
		ret callerLenComptime(d, len(dest.Type.comptimeEnumFields().fields))
	| dest.Type.comptimeTypeInfos() != nil:
		ret callerLenComptime(d, len(dest.Type.comptimeTypeInfos().types))
	| dest.Type.comptimeParams() != nil:
		ret callerLenComptime(d, len(dest.Type.comptimeParams().params))
	| dest.Type.comptimeStatics() != nil:
		ret callerLenComptime(d, len(dest.Type.comptimeStatics().fields))
	| dest.Type.comptimeFiles() != nil:
		ret callerLenComptime(d, len(dest.Type.comptimeFiles().files))
	| dest.Type.comptimeDecls() != nil:
		ret callerLenComptime(d, len(dest.Type.comptimeDecls().decls))
	|:
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidExpr)
		ret nil
	}
}

fn callerCapSlice(mut &expr: &Data, mut &d: &Data): &Data {
	d.Type = lenKind()
	d.Model = &BuiltinCapCallExprModel{
		Expr: expr,
	}
	ret d
}

fn builtinCallerCap(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "obj")
		ret nil
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "cap")
	}
	mut dest := e.evalExpr(fc.Args[0])
	match {
	| dest == nil:
		ret nil
	| dest.Decl:
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidExpr)
		ret nil
	| dest.Type.Slc() != nil:
		ret callerCapSlice(dest, d)
	|:
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidExpr)
		ret nil
	}
}

fn callerDeleteMap(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &expr: &Data, mut &d: &Data): &Data {
	if !expr.Mutable {
		e.pushErr(fc.Args[0].Token, build::LogMsg.MutOperationOnImmut)
	}
	d = buildVoidData()
	mut model := &BuiltinDeleteCallExprModel{
		Dest: expr,
	}
	if len(fc.Args) == 2 {
		mut key := e.evalExpr(fc.Args[1])
		match {
		| key == nil:
			ret nil
		| key.Decl:
			e.pushErr(fc.Args[1].Token, build::LogMsg.InvalidExpr)
			ret nil
		|:
			const Reference = false
			mut keyType := expr.Type.Map().Key
			if !e.s.checkAssignType(Reference, keyType, key, fc.Args[1].Token, e.getOwnerRefers()) {
				ret nil
			}
		}
		model.Key = key
	}
	d.Model = model
	ret d
}

fn builtinCallerDelete(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "map")
		ret nil
	}
	if len(fc.Args) > 2 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "delete")
	}
	mut dest := e.evalExpr(fc.Args[0])
	match {
	| dest == nil:
		ret nil
	| dest.Decl:
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidExpr)
		ret nil
	| dest.Type.Map() != nil:
		ret callerDeleteMap(e, fc, dest, d)
	|:
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidExpr)
		ret nil
	}
}

fn builtinCallerStdComptimeTypeOf(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "type|expr")
		ret nil
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "TypeOf")
	}

	mut d := e.evalExprKind(fc.Args[0].Kind)
	if d == nil {
		ret nil
	}
	f := d.Type.Fn()
	if f != nil {
		if f.IsBuiltin() {
			e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidTypeForTypeOf, "<built-in fn>")
			ret nil
		}
		if len(f.Generics) != len(f.Decl.Generics) {
			e.pushErr(fc.Args[0].Token, build::LogMsg.HasGenerics)
			ret nil
		}
	}
	s := d.Type.Struct()
	if s != nil && len(s.Generics) != len(s.Decl.Generics) {
		e.pushErr(fc.Args[0].Token, build::LogMsg.HasGenerics)
		ret nil
	}
	if d.Type.comptime() {
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidTypeForTypeOf, d.Type.Str())
		ret nil
	}
	ret buildComptimeTypeInfoData(e.s, d.Type)
}

fn builtinCallerStdComptimeValueOf(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "expr")
		ret nil
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "ValueOf")
	}
	mut d := e.evalExpr(fc.Args[0])
	if d == nil {
		ret nil
	}
	// Do not allow value if has unsupported type for type infer.
	if !isGoodValueToInfer(d) {
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidTypeForFn, d.Type.Str(), "ValueOf")
		ret nil
	}
	ret buildComptimeValue(d)
}

fn builtinCallerStdComptimeLine(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) > 0 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "Line")
	}
	mut constant := constant::Const.NewI64(i64(fc.Token.Row))
	ret &Data{
		Type: primInt,
		Constant: constant,
		Model: constant,
	}
}

fn builtinCallerStdComptimeFile(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) > 0 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "File")
	}
	ret &Data{
		Type: &Type{
			Kind: &comptimeFile{
				file: e.s.file,
			},
		},
	}
}

fn builtinCallerStdComptimeFiles(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) > 0 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "Files")
	}
	ret &Data{
		Type: &Type{
			Kind: &comptimeFiles{
				files: e.s.files,
			},
		},
	}
}

fn builtinCallerStdComptimeTypeAlias(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) > 2 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "TypeAlias")
		ret nil
	}
	if len(fc.Args) == 0 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "ident, type")
		ret nil
	}
	if len(fc.Args) == 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "type")
		ret nil
	}
	match type fc.Args[0].Kind {
	| &ast::IdentExpr:
		break
	|:
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidSyntax)
		e.pushSuggestion(build::LogMsg.ExpectedIdentifier)
		ret nil
	}
	mut ident := (&ast::IdentExpr)(fc.Args[0].Kind).Token
	match type e.lookup {
	| &scopeChecker:
		break
	|:
		e.pushErr(fc.Token, build::LogMsg.CalledOutOfScope, "TypeAlias")
		ret nil
	}

	mut sc := (&scopeChecker)(e.lookup)
	mut alias := &TypeAlias{
		Scope: sc.tree,
		Public: mod::IsPub(ident.Kind),
		Binded: false,
		Used: false,
		Generic: false,
		Token: ident,
		Ident: ident.Kind,
		TypeSym: new(TypeSym),
	}
	mut t := e.eval1(fc.Args[1])
	if t == nil {
		ret nil
	}
	if t.Decl {
		alias.TypeSym.Type = t.Type
	} else {
		mut cti := t.Type.comptimeTypeInfo()
		if cti == nil {
			e.pushErr(fc.Args[1].Token, build::LogMsg.InvalidSyntax)
			e.pushSuggestion(build::LogMsg.ExpectedType)
			ret nil
		}
		alias.TypeSym.Type = cti.base
	}
	sc.checkTypeAlias(alias)
	ret buildVoidData()
}

fn builtinCallerStdMemSizeOf(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	mut result := &Data{
		Type: primUint,
	}

	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "type|expr")
		ret result
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "SizeOf")
	}

	mut d := e.evalExprKind(fc.Args[0].Kind)
	if d == nil {
		ret result
	}
	if !validTypeForXof(d.Type) {
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidTypeForFn, d.Type.Str(), "SizeOf")
	}

	result.Model = &SizeofExprModel{Expr: d.Model}
	ret result
}

fn builtinCallerStdMemAlignOf(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	mut result := &Data{
		Type: primUint,
	}

	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "type|expr")
		ret result
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "AlignOf")
	}

	mut d := e.evalExprKind(fc.Args[0].Kind)
	if d == nil {
		ret result
	}
	if !validTypeForXof(d.Type) {
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidTypeForFn, d.Type.Str(), "AlignOf")
	}

	result.Model = &AlignofExprModel{Expr: d.Model}
	ret result
}

fn builtinCallerStdMemFree(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &_: &Data): &Data {
	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "h")
		ret nil
	}
	if len(fc.Args) > 1 {
		e.pushErr(fc.Args[1].Token, build::LogMsg.ArgumentOverflow, "Free")
	}

	mut d := e.evalExpr(fc.Args[0])
	if d == nil {
		ret nil
	}

	if !d.Type.GC() {
		e.pushErr(fc.Args[0].Token, build::LogMsg.InvalidExpr)
	}

	mut result := buildVoidData()
	result.Model = &FreeExprModel{Expr: d.Model}
	ret result
}

fn builtinCallerStdJuleIntegratedEmit(mut &e: &eval, mut &fc: &ast::FnCallExpr, mut &d: &Data): &Data {
	if !e.isUnsafe() {
		e.pushErr(fc.Token, build::LogMsg.UnsafeBehaviorAtOutOfUnsafeScope)
	}

	if len(fc.Args) < 1 {
		e.pushErr(fc.Token, build::LogMsg.MissingExprFor, "code")
		ret nil
	}

	mut argd := e.evalExpr(fc.Args[0])
	if argd == nil {
		ret nil
	}

	if argd.Type.Prim() == nil || !argd.Type.Prim().IsStr() {
		e.pushErr(fc.Args[0].Token, build::LogMsg.IncompatibleTypes, types::Kind.Str, argd.Type.Str())
		ret nil
	}

	if !argd.IsConst() || !argd.Constant.IsStr() {
		e.pushErr(fc.Args[0].Token, build::LogMsg.ExprNotConst)
		ret nil
	}

	mut r := buildVoidData()

	mut f := d.Type.Fn()
	if len(f.Generics) > 0 {
		r.Type = f.Generics[0].Kind
	}

	mut model := &BackendEmitExprModel{
		Code: argd.Constant.ReadStr(),
	}

	if len(fc.Args) > 1 {
		model.Exprs = make([]ExprModel, 0, len(fc.Args)-1)
		for (_, mut arg) in fc.Args[1:] {
			argd = e.eval1(arg)
			if argd == nil {
				ret nil
			}
			model.Exprs = append(model.Exprs, argd.Model)
		}
	}

	r.Model = model
	ret r
}