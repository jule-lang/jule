// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/jule/ast"
use "std/jule/build"
use "std/jule/token"

// Eval directive expression.
// Directive expressions are logial expressions and uses varaibles.
//
// Read manual for more information about syntax or etc:
// https://manual.jule.dev/compiler/directives.html#directive-expressions
struct directiveEval {
	logs: []build::Log
	d:    &ast::Directive
	vars: []str
}

impl directiveEval {
	fn pushErr(mut self, t: &token::Token, fmt: build::LogMsg, args: ...any) {
		self.logs = append(self.logs, build::Log{
			Kind: build::LogKind.Error,
			Row: t.Row,
			Column: t.Column,
			Path: t.File.Path,
			Text: build::Logf(fmt, args...),
		})
	}

	// Split operans by binary operators.
	// Skips range of parentheses.
	fn splitOperans(mut self, op: token::Id, mut &tokens: []&token::Token): [][]&token::Token {
		mut parts := make([][]&token::Token, 0, 10)
		mut i := 0
		mut rangeN := 0
		mut last := 0
		for i < len(tokens)-1; i++ {
			b := tokens[i]
			match b.Id {
			| token::Id.LParent:
				rangeN++
				continue
			| token::Id.RParent:
				rangeN--
			}
			if rangeN > 0 || b.Id != op {
				continue
			}
			parts = append(parts, tokens[last:i])
			last = i + 1
		}
		if last < len(tokens) {
			parts = append(parts, tokens[last:])
		} else {
			// Missing operand.
			self.pushErr(tokens[last-1], build::LogMsg.MissingExpr)
			ret nil
		}
		ret parts
	}

	// Eval directive variable.
	fn evalDirectiveIdent(self, ident: str): bool {
		if ident == "" {
			ret false
		}
		for _, var in self.vars {
			if var == ident {
				ret true
			}
		}
		ret false
	}

	// Eval directive expression part.
	// Accepts unary operators.
	fn evalDirectivePart(mut self, mut part: []&token::Token): bool {
		logicalNot := part[0].Id == token::Id.Excl
		if logicalNot {
			part = part[1:]
		}

		if len(part) > 1 {
			self.pushErr(part[1], build::LogMsg.InvalidSyntax)
			ret false
		}

		mut result := self.evalDirectiveIdent(part[0].Kind)
		if logicalNot {
			result = !result
		}
		ret result
	}

	fn eval(mut self): bool {
		if len(self.d.Args) == 0 {
			self.pushErr(self.d.Tag, build::LogMsg.MissingExpr)
			ret false
		}

		mut result := false

		mut logicalOrParts := self.splitOperans(token::Id.DblVline, self.d.Args)
		if logicalOrParts == nil {
			ret false
		}

		for (_, mut part) in logicalOrParts {
			mut and := true
			mut logicalAndParts := self.splitOperans(token::Id.DblAmper, part)
			if logicalAndParts == nil {
				ret false
			}

			for (_, mut andPart) in logicalAndParts {
				first := andPart[0]
				if first.Id == token::Id.LParent {
					end := andPart[len(andPart)-1]

					// Missing close.
					if end.Id != token::Id.RParent {
						self.pushErr(first, build::LogMsg.WaitCloseParent)
						ret false
					}

					andPart = andPart[1:len(andPart)-1] // Remove parentheses.
				}
				and = and && self.evalDirectivePart(andPart)
			}

			result = result || and
		}

		ret result
	}
}