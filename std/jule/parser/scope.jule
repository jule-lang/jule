// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::jule::ast::{
    ScopeTree,
    Node,
    NodeData,
    RetSt,
    Iter,
    WhileKind,
    VarDecl,
    RangeKind,
    IterKind,
    BreakSt,
    ContSt,
    If,
    Else,
    Conditional,
    FnCallExpr,
    Expr,
    GotoSt,
    FallSt,
    Case,
    MatchCase,
    LabelSt,
    AssignLeft,
    AssignSt,
    UseExpr,
    Stmt,
    StmtData,
    TypeAliasDecl,
}
use std::jule::build::{LogMsg}
use std::jule::lex::{
    Token,
    TokenId,
    TokenKind,
    IsAssignOp,
    IsPostfixOp,
    IsBinOp,
}

fn newScope(): &ScopeTree {
    ret new(ScopeTree)
}

// Reports whether token is statement finish point.
fn isSt(current: &Token, prev: &Token): (ok: bool, terminated: bool) {
    ok = current.Id == TokenId.Semicolon || prev.Row < current.Row
    terminated = current.Id == TokenId.Semicolon
    ret
}

fn prevIsIncompleteExpr(&tokens: []&Token, &i: int): bool {
    // Ignore namespaces.
    if i > 1 && tokens[i-2].Id == TokenId.DblColon {
        ret false
    }
    unsafe {
        let prev = tokens[i-1]
        ret prev.Id == TokenId.Dot ||
            (IsBinOp(prev.Id) && prev.Row < tokens[i].Row)
    }
}

// Reports position of the next statement if exist, len(toks) if not.
fn nextStPos(&tokens: []&Token, start: int): (int, bool) {
    let mut braceN = 0
    let mut i = start
    for i < len(tokens); i++ {
        let mut ok: bool = false
        let mut terminated: bool = false
        let tok = tokens[i]
        match tok.Id {
        | TokenId.Range:
            match tok.Kind {
            | TokenKind.LBrace
            | TokenKind.LBracket
            | TokenKind.LParent:
                if braceN == 0 && i > start {
                    if !prevIsIncompleteExpr(tokens, i) {
                        ok, terminated = isSt(tok, tokens[i-1])
                        if ok {
                            goto return
                        }
                    }
                }
                braceN++
            |:
                braceN--
                if braceN == 0 && i+1 < len(tokens) {
                    ok, terminated = isSt(tokens[i+1], tok)
                    if ok {
                        i++
                        goto return
                    }
                }
            }
            continue
        }

        if braceN != 0 {
            continue
        }

        if i > start {
            if prevIsIncompleteExpr(tokens, i) {
                continue
            }
            ok, terminated = isSt(tok, tokens[i-1])
        } else {
            ok, terminated = isSt(tok, tok)
        }
        if !ok {
            continue
        }

    return:
        if terminated {
            i++
        }
        ret i, terminated
    }
    ret i, false
}

// Returns current statement tokens.
// Starts selection at i.
fn skipSt(mut &i: int, mut tokens: []&Token): ([]&Token, bool) {
    let start = i
    let mut terminated = false
    i, terminated = nextStPos(tokens, start)
    let mut stTokens = tokens[start:i]
    if terminated {
        if len(stTokens) == 1 {
            stTokens, terminated = skipSt(i, tokens)
            ret stTokens, terminated
        }
        // -1 for eliminate statement terminator.
        stTokens = stTokens[:len(stTokens)-1]
    }
    ret stTokens, terminated
}

struct stmt {
    tokens:     []&Token
    terminated: bool
}

// Splits all statements.
fn splitStmts(mut &tokens: []&Token): []&stmt {
    let mut stmts = make([]&stmt, 0, 20)
    let mut pos = 0
    for pos < len(tokens) {
        let (mut stmt, terminated) = skipSt(pos, tokens)
        stmts = append(stmts, &stmt{
            tokens: stmt,
            terminated: terminated,
        })
    }
    ret stmts
}

struct scopeParser {
    p:     &parser
    s:     &ScopeTree
    stmts: []&stmt
    pos:   int
}

impl scopeParser {
    fn stop(mut self) {
        self.pos = -1
    }

    fn stopped(self): bool {
        ret self.pos == -1
    }

    fn finished(self): bool {
        ret self.pos >= len(self.stmts)
    }

    fn isLastSt(self): bool {
        ret self.pos+1 >= len(self.stmts)
    }

    fn pushErr(mut self, token: &Token, fmt: LogMsg) {
        self.p.pushErr(token, fmt)
    }

    // Push suggestion to last log.
    fn pushSuggestion(mut self, fmt: LogMsg, args: ...any) {
        self.p.pushSuggestion(fmt, args...)
    }

    fn insertAsNext(mut self, mut &tokens: []&Token) {
        self.stmts = append(self.stmts[:self.pos+1], self.stmts[self.pos:]...)
        self.stmts[self.pos+1] = &stmt{tokens: tokens}
    }

    fn next(mut self): &stmt {
        self.pos++
        ret self.stmts[self.pos]
    }

    fn buildScope(mut self, mut &tokens: []&Token, mut end: &Token): &ScopeTree {
        let mut s = newScope()
        s.Parent = self.s
        s.End = end
        let mut ssp = scopeParser{
            p: self.p,
        }
        ssp.build(tokens, s)
        ret s
    }

    fn buildVarSt(mut self, mut &tokens: []&Token): &VarDecl {
        let mut v = self.p.buildVar(tokens)
        v.Scope = self.s
        ret v
    }

    fn buildRetSt(mut self, mut tokens: []&Token): &RetSt {
        let mut st = &RetSt{
            Token: tokens[0],
        }
        if len(tokens) > 1 {
            tokens = tokens[1:] // Remove ret keyword.
            st.Expr = self.p.buildExpr(tokens)
        }
        ret st
    }

    fn buildWhileNextIter(mut self, mut &s: &stmt): &Iter {
        let mut it = &Iter{
            Token: s.tokens[0],
        }
        let mut tokens = s.tokens[1:] // Skip "iter" keyword.
        let mut kind = new(WhileKind)

        if len(tokens) > 0 {
            kind.Expr = self.p.buildExpr(tokens)
        }

        if self.isLastSt() {
            self.pushErr(it.Token, LogMsg.InvalidSyntax)
            ret nil
        }

        tokens = self.next().tokens
        let mut stTokens = getBlockExpr(tokens)
        if len(stTokens) > 0 {
            let mut stmt = &stmt{
                terminated: s.terminated,
                tokens: stTokens,
            }
            kind.NextToken = stTokens[0]
            kind.Next = self.buildSt(stmt)
        }

        let mut i = len(stTokens)
        let mut blockTokens = range(i, TokenKind.LBrace, TokenKind.RBrace, tokens)
        if blockTokens == nil {
            self.stop()
            self.pushErr(it.Token, LogMsg.BodyNotExist)
            self.pushSuggestion(LogMsg.ExpectedBody)
            ret nil
        }
        if i < len(tokens) {
            self.pushErr(tokens[i], LogMsg.InvalidSyntax)
        }

        it.Scope = self.buildScope(blockTokens, tokens[i-1])
        it.Kind = kind

        ret it
    }

    fn buildWhileIterKind(mut self, mut &tokens: []&Token): &WhileKind {
        ret &WhileKind{
            Expr: self.p.buildExpr(tokens),
        }
    }

    fn getRangeKindKeysTokens(mut self, mut &toks: []&Token): [][]&Token {
        let (mut vars, errs) = parts(toks, TokenId.Comma, true)
        self.p.errors = append(self.p.errors, errs...)
        ret vars
    }

    fn buildRangeKindKey(mut self, mut &tokens: []&Token): &VarDecl {
        if len(tokens) == 0 {
            ret nil
        }
        let mut key = &VarDecl{
            Token: tokens[0],
        }
        if key.Token.Id == TokenId.Mut {
            key.Mutable = true
            if len(tokens) == 1 {
                self.pushErr(key.Token, LogMsg.InvalidSyntax)
            }
            key.Token = tokens[1]
        } else if len(tokens) > 1 {
            self.pushErr(tokens[1], LogMsg.InvalidSyntax)
        }
        if key.Token.Id != TokenId.Ident {
            self.pushErr(key.Token, LogMsg.InvalidSyntax)
            self.pushSuggestion(LogMsg.ExpectedIdentifier)
            ret nil
        }
        key.Ident = key.Token.Kind
        ret key
    }

    fn buildRangeKindKeys(mut self, mut &parts: [][]&Token): []&VarDecl {
        let mut keys: []&VarDecl = nil
        for (_, mut tokens) in parts {
            keys = append(keys, self.buildRangeKindKey(tokens))
        }
        ret keys
    }

    fn setupRangeKindKeysPlain(mut self, mut &rng: &RangeKind, mut &tokens: []&Token) {
        let mut keyTokens = self.getRangeKindKeysTokens(tokens)
        if len(keyTokens) == 0 {
            ret
        }
        if len(keyTokens) > 2 {
            self.pushErr(rng.InToken, LogMsg.MuchRangeVars)
        }
        let mut keys = self.buildRangeKindKeys(keyTokens)
        rng.KeyA = keys[0]
        if len(keys) > 1 {
            rng.KeyB = keys[1]
        }
    }

    fn setupRangeKindKeysExplicit(mut self, mut &rng: &RangeKind, mut &tokens: []&Token) {
        let mut i = 0
        let mut rang = range(i, TokenKind.LParent, TokenKind.RParent, tokens)
        if i < len(tokens) {
            self.pushErr(rng.InToken, LogMsg.InvalidSyntax)
        }
        self.setupRangeKindKeysPlain(rng, rang)
    }

    fn setupRangeKindKeys(mut self, mut &rng: &RangeKind, mut &tokens: []&Token) {
        if tokens[0].Id == TokenId.Range {
            if tokens[0].Kind != TokenKind.LParent {
                self.pushErr(tokens[0], LogMsg.InvalidSyntax)
                ret
            }
            self.setupRangeKindKeysExplicit(rng, tokens)
            ret
        }
        self.setupRangeKindKeysPlain(rng, tokens)
    }

    fn buildRangeIterKind(mut self, mut &var_tokens: []&Token,
        mut &exprTokens: []&Token, mut &inToken: &Token): &RangeKind {
        let mut rng = &RangeKind{
            InToken: inToken,
        }
        if len(exprTokens) == 0 {
            self.pushErr(rng.InToken, LogMsg.MissingExpr)
            ret rng
        }
        rng.Expr = self.p.buildExpr(exprTokens)
        if len(var_tokens) > 0 {
            self.setupRangeKindKeys(rng, var_tokens)
        }
        ret rng
    }

    fn buildCommonIterKind(mut self, mut &tokens: []&Token, &err_tok: &Token): IterKind {
        let mut braceN = 0
        for (i, mut tok) in tokens {
            if tok.Id == TokenId.Range {
                match tok.Kind {
                | TokenKind.LBrace
                | TokenKind.LBracket
                | TokenKind.LParent:
                    braceN++
                    continue
                |:
                    braceN--
                }
            }
            if braceN != 0 {
                continue
            }
            match tok.Id {
            | TokenId.In:
                let mut declTokens = tokens[:i]
                let mut exprTokens = tokens[i+1:]
                ret self.buildRangeIterKind(declTokens, exprTokens, tok)
            }
        }
        ret self.buildWhileIterKind(tokens)
    }

    fn buildCommonIter(mut self, mut tokens: []&Token): &Iter {
        let mut it = &Iter{
            Token: tokens[0],
        }
        tokens = tokens[1:] // Skip "iter" keyword.
        if len(tokens) == 0 {
            self.stop()
            self.pushErr(it.Token, LogMsg.BodyNotExist)
            self.pushSuggestion(LogMsg.ExpectedBody)
            ret nil
        }
        let mut exprTokens = getBlockExpr(tokens)
        if len(exprTokens) > 0 {
            it.Kind = self.buildCommonIterKind(exprTokens, it.Token)
        }
        let mut i = len(exprTokens)
        let mut scopeTokens = range(i, TokenKind.LBrace, TokenKind.RBrace, tokens)
        if scopeTokens == nil {
            self.stop()
            self.pushErr(it.Token, LogMsg.BodyNotExist)
            self.pushSuggestion(LogMsg.ExpectedBody)
            ret nil
        }
        if i < len(tokens) {
            self.pushErr(tokens[i], LogMsg.InvalidSyntax)
        }
        it.Scope = self.buildScope(scopeTokens, tokens[i-1])
        ret it
    }

    fn buildIterSt(mut self, mut &st: &stmt): &Iter {
        if st.terminated {
            ret self.buildWhileNextIter(st)
        }
        ret self.buildCommonIter(st.tokens)
    }

    fn buildBreakSt(mut self, mut &tokens: []&Token): &BreakSt {
        let mut brk = &BreakSt{
            Token: tokens[0],
        }
        if len(tokens) > 1 {
            if tokens[1].Id != TokenId.Ident {
                self.pushErr(tokens[1], LogMsg.InvalidSyntax)
                self.pushSuggestion(LogMsg.ExpectedLabelIdent)
            } else {
                brk.Label = tokens[1]
                if len(tokens) > 2 {
                    self.pushErr(tokens[1], LogMsg.InvalidSyntax)
                }
            }
        }
        ret brk
    }

    fn buildContSt(mut self, mut &tokens: []&Token): &ContSt {
        let mut cont = &ContSt{
            Token: tokens[0],
        }
        if len(tokens) > 1 {
            if tokens[1].Id != TokenId.Ident {
                self.pushErr(tokens[1], LogMsg.InvalidSyntax)
                self.pushSuggestion(LogMsg.ExpectedLabelIdent)
            } else {
                cont.Label = tokens[1]
                if len(tokens) > 2 {
                    self.pushErr(tokens[1], LogMsg.InvalidSyntax)
                }
            }
        }
        ret cont
    }

    fn buildIf(mut self, mut &tokens: []&Token): &If {
        let mut model = &If{
            Token: tokens[0],
        }
        tokens = tokens[1:]
        let mut exprTokens = getBlockExpr(tokens)
        let mut i = 0
        if len(exprTokens) == 0 {
            self.pushErr(model.Token, LogMsg.MissingExpr)
        } else {
            i = len(exprTokens)
        }
        let mut scopeTokens = range(i, TokenKind.LBrace, TokenKind.RBrace, tokens)
        if scopeTokens == nil {
            self.stop()
            self.pushErr(model.Token, LogMsg.BodyNotExist)
            self.pushSuggestion(LogMsg.ExpectedBody)
            ret nil
        }
        let mut end = tokens[i-1]
        if i < len(tokens) {
            if tokens[i].Id == TokenId.Else {
                tokens = tokens[i:]
            } else {
                self.pushErr(tokens[i], LogMsg.InvalidSyntax)
                tokens = nil
            }
        }
        model.Expr = self.p.buildExpr(exprTokens)
        model.Scope = self.buildScope(scopeTokens, end)
        ret model
    }

    fn buildElse(mut self, mut &tokens: []&Token): &Else {
        let mut els = &Else{
            Token: tokens[0],
        }
        tokens = tokens[1:] // Remove "else" keyword.
        let mut i = 0
        let mut scopeTokens = range(i, TokenKind.LBrace, TokenKind.RBrace, tokens)
        if scopeTokens == nil {
            if i < len(tokens) {
                self.pushErr(els.Token, LogMsg.ElseHaveExpr)
            } else {
                self.stop()
                self.pushErr(els.Token, LogMsg.BodyNotExist)
                self.pushSuggestion(LogMsg.ExpectedBody)
            }
            ret nil
        }
        if i < len(tokens) {
            self.pushErr(tokens[i], LogMsg.InvalidSyntax)
        }
        els.Scope = self.buildScope(scopeTokens, tokens[i-1])
        ret els
    }

    fn buildIfElseChain(mut self, mut tokens: []&Token): &Conditional {
        let mut chain = &Conditional{
            Head: self.buildIf(tokens),
        }
        if chain.Head == nil {
            ret nil
        }
        for len(tokens) != 0 {
            if tokens[0].Id != TokenId.Else {
                break
            }
            if len(tokens) > 1 && tokens[1].Id == TokenId.If {
                tokens = tokens[1:] // Remove else token
                let mut elif = self.buildIf(tokens)
                chain.Tail = append(chain.Tail, elif)
                continue
            }
            chain.Default = self.buildElse(tokens)
            break
        }
        ret chain
    }

    fn buildCoCallSt(mut self, mut tokens: []&Token): &Expr {
        let token = tokens[0]
        tokens = tokens[1:] // Start 1 to skip "co" token.
        let mut e = self.p.buildExpr(tokens)
        if e == nil {
            ret e
        }
        match type e.Kind {
        | &FnCallExpr:
            (&FnCallExpr)(e.Kind).IsCo = true
        |:
            self.pushErr(token, LogMsg.ExprNotFnCall)
        }
        ret e
    }

    fn buildGotoSt(mut self, mut &tokens: []&Token): &GotoSt {
        let mut gt = &GotoSt{
            Token: tokens[0],
        }
        if len(tokens) == 1 {
            self.pushErr(gt.Token, LogMsg.MissingGotoLabel)
            ret nil
        } else if len(tokens) > 2 {
            self.pushErr(tokens[2], LogMsg.InvalidSyntax)
        }
        let mut identToken = tokens[1]
        if identToken.Id != TokenId.Ident {
            self.pushErr(identToken, LogMsg.InvalidSyntax)
            self.pushSuggestion(LogMsg.ExpectedIdentifier)
            ret gt
        }
        gt.Label = identToken
        ret gt
    }

    fn buildFallSt(mut self, mut &tokens: []&Token): &FallSt {
        let mut fll = &FallSt{
            Token: tokens[0],
        }
        if len(tokens) > 1 {
            self.pushErr(tokens[1], LogMsg.InvalidSyntax)
        }
        ret fll
    }

    fn buildTypeAliasSt(mut self, mut &tokens: []&Token): &TypeAliasDecl {
        let mut tad = self.p.buildTypeAliasDecl(tokens)
        tad.Scope = self.s
        ret tad
    }

    fn pushCaseExpr(mut self, mut tokens: []&Token, mut token: &Token,
        typeMatch: bool, mut &exprs: []&Expr) {
        if len(tokens) == 0 {
            ret
        }

        if typeMatch {
            let mut i = 0
            let (mut t, ok) = unsafe { self.p.buildType(tokens, &i, true) }
            if ok {
                exprs = append(exprs, &Expr{
                    Token: tokens[0],
                    End: tokens[len(tokens)-1],
                    Kind: t,
                })
                if i < len(tokens) {
                    self.pushErr(tokens[i], LogMsg.InvalidSyntax)
                }
            }
            ret
        }
        exprs = append(exprs, self.p.buildExpr(tokens))
    }

    fn buildCaseExprs(mut self, mut &tokens: []&Token, mut &colon: &Token, typeMatch: bool): []&Expr {
        let mut exprs = make([]&Expr, 0, 1)

        let mut braceN = 0
        let mut j = 0
        for (i, mut tok) in tokens {
            if tok.Id == TokenId.Range {
                match tok.Kind {
                | TokenKind.LParent
                | TokenKind.LBrace
                | TokenKind.LBracket:
                    braceN++
                |:
                    braceN--
                }
                continue
            } else if braceN != 0 {
                continue
            }
            match {
            | tok.Id == TokenId.Vline:
                let exprTokens = tokens[j:i]
                if len(exprTokens) == 0 {
                    self.pushErr(tok, LogMsg.MissingExpr)
                } else {
                    self.pushCaseExpr(tokens[j:i], tok, typeMatch, exprs)
                }
                j = i + 1
            | tok.Id == TokenId.Colon:
                colon = tok
                self.pushCaseExpr(tokens[j:i], tok, typeMatch, exprs)
                tokens = tokens[i+1:]
                ret exprs
            }
        }
        self.pushErr(tokens[0], LogMsg.InvalidSyntax)
        tokens = nil
        ret nil
    }

    fn buildCaseScope(mut self, mut &tokens: []&Token): &ScopeTree {
        let mut n = 0
        for {
            let mut i = 0
            let (mut next, _) = skipSt(i, tokens[n:])
            if len(next) == 0 {
                break
            }
            let tok = next[0]
            if tok.Id != TokenId.Vline {
                n += i
                continue
            }
            let mut scopeTokens = tokens[:n]
            let mut scope = self.buildScope(scopeTokens, tokens[n])
            tokens = tokens[n:]
            ret scope
        }
        let mut end: &Token
        if len(tokens) > 0 {
            end = tokens[len(tokens)-1]
        }
        let mut scope = self.buildScope(tokens, end)
        tokens = nil
        ret scope
    }

    fn buildCase(mut self, mut &tokens: []&Token, typeMatch: bool): (&Case, bool) {
        let mut c = &Case{
            Token: tokens[0],
        }
        tokens = tokens[1:] // Remove case prefix.
        let mut colon: &Token = nil
        c.Exprs = self.buildCaseExprs(tokens, colon, typeMatch)
        c.Scope = self.buildCaseScope(tokens)
        if c.Scope.End == nil {
            c.Scope.End = colon
        }
        let isDefault = len(c.Exprs) == 0
        ret c, isDefault
    }

    fn buildCases(mut self, mut tokens: []&Token, typeMatch: bool): ([]&Case, &Else) {
        let mut cases: []&Case = nil
        let mut def: &Else = nil
        let mut defNotLast = false
        for len(tokens) > 0 {
            let mut tok = tokens[0]
            if tok.Id != TokenId.Vline {
                self.pushErr(tok, LogMsg.InvalidSyntax)
                break
            }
            let (mut c, isDefault) = self.buildCase(tokens, typeMatch)
            if isDefault {
                c.Token = tok
                if def == nil {
                    def = &Else{
                        Token: c.Token,
                        Scope: c.Scope,
                    }
                } else {
                    self.pushErr(tok, LogMsg.InvalidSyntax)
                }
            } else {
                defNotLast = defNotLast || def != nil
                cases = append(cases, c)
            }
        }

        if defNotLast {
            self.pushErr(def.Token, LogMsg.DefaultNotLast)
        }

        ret cases, def
    }

    fn buildMatchCase(mut self, mut tokens: []&Token): &MatchCase {
        let mut m = &MatchCase{
            Token: tokens[0],
        }
        tokens = tokens[1:] // Remove "match" keyword.

        if len(tokens) > 0 && tokens[0].Id == TokenId.Type {
            m.TypeMatch = true
            tokens = tokens[1:] // Skip "type" keyword
        }

        let mut exprTokens = getBlockExpr(tokens)
        if len(exprTokens) > 0 {
            m.Expr = self.p.buildExpr(exprTokens)
        } else if m.TypeMatch {
            self.pushErr(m.Token, LogMsg.MissingExpr)
        }

        let mut i = len(exprTokens)
        let mut blockToks = range(i, TokenKind.LBrace, TokenKind.RBrace, tokens)
        if blockToks == nil {
            self.stop()
            self.pushErr(m.Token, LogMsg.BodyNotExist)
            self.pushSuggestion(LogMsg.ExpectedBody)
            ret nil
        } else if i < len(tokens) {
            self.pushErr(tokens[i], LogMsg.InvalidSyntax)
        }
        m.End = tokens[i-1]
        m.Cases, m.Default = self.buildCases(blockToks, m.TypeMatch)
        ret m
    }

    fn buildScopeSt(mut self, mut tokens: []&Token): &ScopeTree {
        let mut isUnsafe = false
        let mut isDeferred = false
        let mut token = tokens[0]
        if token.Id == TokenId.Unsafe {
            isUnsafe = true
            tokens = tokens[1:]
            if len(tokens) == 0 {
                self.pushErr(token, LogMsg.InvalidSyntax)
                ret nil
            }
            token = tokens[0]
            if token.Id == TokenId.Defer {
                isDeferred = true
                tokens = tokens[1:]
                if len(tokens) == 0 {
                    self.pushErr(token, LogMsg.InvalidSyntax)
                    ret nil
                }
            }
        } else if token.Id == TokenId.Defer {
            isDeferred = true
            tokens = tokens[1:]
            if len(tokens) == 0 {
                self.pushErr(token, LogMsg.InvalidSyntax)
                ret nil
            }
        }

        let mut i = 0
        let mut scopeTokens = range(i, TokenKind.LBrace, TokenKind.RBrace, tokens)
        if scopeTokens == nil {
            self.pushErr(token, LogMsg.InvalidSyntax)
            ret nil
        } else if i < len(tokens) {
            self.pushErr(tokens[i], LogMsg.InvalidSyntax)
        }
        let mut scope = self.buildScope(scopeTokens, tokens[i-1])
        scope.Unsafety = isUnsafe
        scope.Deferred = isDeferred
        ret scope
    }

    fn buildLabelSt(mut self, mut &tokens: []&Token): &LabelSt {
        let mut lbl = &LabelSt{
            Token: tokens[0],
            Ident: tokens[0].Kind,
        }

        // Save followed statement
        if len(tokens) > 2 {
            tokens = tokens[2:] // Remove goto keyword and label
            self.insertAsNext(tokens)
        }

        ret lbl
    }

    fn buildIdSt(mut self, mut &tokens: []&Token): (StmtData, ok: bool) {
        if len(tokens) == 1 {
            ret
        }

        let mut token = tokens[1]
        match token.Id {
        | TokenId.Colon:
            ret self.buildLabelSt(tokens), true
        }

        ret
    }

    fn buildAssignInfo(mut self, mut &tokens: []&Token): &assignInfo {
        let mut info = &assignInfo{
            ok: true,
        }
        let mut braceN = 0
        for (i, mut token) in tokens {
            if token.Id == TokenId.Range {
                match token.Kind {
                | TokenKind.LBrace
                | TokenKind.LBracket
                | TokenKind.LParent:
                    braceN++
                |:
                    braceN--
                }
            }
            match {
            | braceN > 0:
                continue
            | !IsAssignOp(token.Id) && token.Id != TokenId.ColonEq:
                continue
            }

            info.l = tokens[:i]
            if len(info.l) == 0 {
                info.ok = false
            }
            info.setter = token
            if i+1 >= len(tokens) {
                info.r = nil
                info.ok = IsPostfixOp(info.setter.Id)
                break
            }
            info.r = tokens[i+1:]
            if IsPostfixOp(info.setter.Id) {
                if len(info.r) > 0 {
                    self.pushErr(info.r[0], LogMsg.InvalidSyntax)
                    info.r = nil
                }
            }
            break
        }
        ret info
    }

    fn buildAssignL(mut self, mut &tokens: []&Token): &AssignLeft {
        let mut l = &AssignLeft{
            Token: tokens[0],
        }
        if tokens[0].Id == TokenId.Ident {
            l.Ident = l.Token.Kind
        }
        l.Expr = self.p.buildExpr(tokens)
        ret l
    }

    fn buildAssignLs(mut self, mut &parts: [][]&Token): []&AssignLeft {
        let mut lefts: []&AssignLeft = nil
        for (_, mut part) in parts {
            let mut l = self.buildAssignL(part)
            lefts = append(lefts, l)
        }

        ret lefts
    }

    fn buildPlainAssign(mut self, mut &tokens: []&Token): (&AssignSt, bool) {
        let mut info = self.buildAssignInfo(tokens)
        if !info.ok {
            ret nil, false
        }

        let mut assign = &AssignSt{
            Setter: info.setter,
        }

        // Caught declaration assignments.
        if info.setter.Id == TokenId.ColonEq {
            assign.Declarative = true
            assign.Right = self.p.buildExpr(info.r)
            let ok = self.buildDeclAssign1(info.l, assign)
            ret assign, ok
        }

        let (mut parts, errs) = parts(info.l, TokenId.Comma, true)
        if len(errs) > 0 {
            self.p.errors = append(self.p.errors, errs...)
            ret nil, false
        }

        assign.Left = self.buildAssignLs(parts)
        if info.r != nil {
            assign.Right = self.p.buildExpr(info.r)
        }

        ret assign, true
    }

    fn buildDeclAssign1(mut self, mut &lefts: []&Token, mut &assign: &AssignSt): bool {
        // Lefts
        let (mut parts, errs) = parts(lefts, TokenId.Comma, true)
        if len(errs) > 0 {
            self.p.errors = append(self.p.errors, errs...)
            ret false
        }

        for (_, mut part) in parts {
            let mut isMut = false
            let mut isRef = false

            let token = part[0]
            if token.Id == TokenId.Mut {
                isMut = true
                part = part[1:]
                if len(part) == 0 {
                    self.pushErr(token, LogMsg.InvalidSyntax)
                    continue
                }
            }

            if part[0].Id == TokenId.Amper {
                isRef = true
                part = part[1:]
                if len(part) == 0 {
                    self.pushErr(token, LogMsg.InvalidSyntax)
                    continue
                }
            }

            if (part[0].Id != TokenId.Ident &&
                part[0].Id != TokenId.Range &&
                part[0].Kind != TokenKind.LParent) {
                self.pushErr(token, LogMsg.InvalidSyntax)
                continue
            }

            let mut l = self.buildAssignL(part)
            l.Mutable = isMut
            l.Reference = isRef
            assign.Left = append(assign.Left, l)
        }

        ret true
    }

    fn buildDeclAssign(mut self, mut tokens: []&Token): (&AssignSt, bool) {
        if len(tokens) < 1 {
            ret nil, false
        }

        tokens = tokens[1:] // Skip "let" keyword
        let mut token = tokens[0]
        if token.Id != TokenId.Range || token.Kind != TokenKind.LParent {
            ret nil, false
        }

        let mut assign = &AssignSt{
            Declarative: true,
        }

        let mut i = 0
        let mut rang = range(i, TokenKind.LParent, TokenKind.RParent, tokens)
        if rang == nil {
            self.pushErr(token, LogMsg.InvalidSyntax)
            ret nil, false
        } else if i+1 < len(tokens) {
            assign.Setter = tokens[i]
            if assign.Setter.Id != TokenId.Eq {
                self.pushErr(assign.Setter, LogMsg.InvalidSyntax)
            }
            i++
            let mut exprTokens = tokens[i:]
            assign.Right = self.p.buildExpr(exprTokens)
        }

        let ok = self.buildDeclAssign1(rang, assign)
        ret assign, ok
    }

    fn buildAssignSt(mut self, mut &tokens: []&Token): (st: &AssignSt, ok: bool) {
        if !checkAssignTokens(tokens) {
            ret nil, false
        }

        match tokens[0].Id {
        | TokenId.Let:
            st, ok = self.buildDeclAssign(tokens)
        |:
            st, ok = self.buildPlainAssign(tokens)
        }

        ret
    }

    fn buildUseExpr(mut self, mut &tokens: []&Token): &UseExpr {
        if len(tokens) == 1 {
            self.pushErr(tokens[0], LogMsg.MissingExpr)
            ret nil
        }
        let mut ue = &UseExpr{
            Token: tokens[0],
        }
        tokens = tokens[1:] // Ignore "use" keyword.
        ue.Expr = self.p.buildExpr(tokens)
        ret ue
    }

    fn buildSt(mut self, mut &st: &stmt): StmtData {
        let mut token = st.tokens[0]
        if token.Id == TokenId.Ident {
            let (mut s, ok) = self.buildIdSt(st.tokens)
            if ok {
                ret s
            }
        }

        let (mut s, ok) = self.buildAssignSt(st.tokens)
        if ok {
            ret s
        }

        match token.Id {
        | TokenId.Use:
            ret self.buildUseExpr(st.tokens)
        | TokenId.Static
        | TokenId.Const
        | TokenId.Let
        | TokenId.Mut:
            ret self.buildVarSt(st.tokens)
        | TokenId.Ret:
            ret self.buildRetSt(st.tokens)
        | TokenId.For:
            ret self.buildIterSt(st)
        | TokenId.Break:
            ret self.buildBreakSt(st.tokens)
        | TokenId.Cont:
            ret self.buildContSt(st.tokens)
        | TokenId.If:
            ret self.buildIfElseChain(st.tokens)
        | TokenId.Co:
            ret self.buildCoCallSt(st.tokens)
        | TokenId.Goto:
            ret self.buildGotoSt(st.tokens)
        | TokenId.Fall:
            ret self.buildFallSt(st.tokens)
        | TokenId.Type:
            ret self.buildTypeAliasSt(st.tokens)
        | TokenId.Match:
            ret self.buildMatchCase(st.tokens)
        | TokenId.Unsafe
        | TokenId.Defer:
            ret self.buildScopeSt(st.tokens)
        | TokenId.Range:
            if token.Kind == TokenKind.LBrace {
                ret self.buildScopeSt(st.tokens)
            }
        }
        let mut expr = self.p.buildExpr(st.tokens)
        if expr != nil {
            ret expr
        }
        self.pushErr(token, LogMsg.InvalidSyntax)
        ret nil
    }

    fn build(mut self, mut &tokens: []&Token, mut &s: &ScopeTree) {
        if s == nil {
            ret
        }
        self.stmts = splitStmts(tokens)
        self.pos = -1 // sp.next() first increase position
        self.s = s
        for !self.isLastSt() && !self.finished() {
            let mut st = self.next()
            let mut data = self.buildSt(st)
            if data != nil {
                self.s.Stmts = append(self.s.Stmts, Stmt{
                    Token: st.tokens[0],
                    Data: data,
                })
            }
            if self.stopped() {
                break
            }
        }
    }
}