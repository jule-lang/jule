// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use ast for std::jule::ast::{
	TypeDecl,
	SubIdentTypeDecl,
	IdentTypeDecl,
	NamespaceTypeDecl,
	PtrTypeDecl,
	SptrTypeDecl,
	SlcTypeDecl,
	ArrTypeDecl,
	MapTypeDecl,
}
use std::jule::build::{LogMsg}
use std::jule::lex::{Token, TokenId, TokenKind}

fn buildVoidType(): &TypeDecl {
	ret new(TypeDecl)
}

fn buildPrimType(mut t: &Token): &TypeDecl {
	ret &TypeDecl{
		Token: t,
		Kind: &IdentTypeDecl{
			Token: t,
			Ident: t.Kind,
		},
	}
}

struct typeBuilder {
	p:      &parser
	tokens: []&Token
	i:      *int
	err:    bool
}

impl typeBuilder {
	fn pushErr(mut self, token: &Token, fmt: LogMsg) {
		if self.err {
			self.p.pushErr(token, fmt)
		}
	}

	// Push suggestion to last log.
	fn pushSuggestion(mut self, fmt: LogMsg, args: ...any) {
		if self.err {
			self.p.pushSuggestion(fmt, args...)
		}
	}

	unsafe fn buildNamespace(mut self): &TypeDecl {
		mut t := &TypeDecl{
			Token: self.tokens[*self.i],
		}

		mut nst := new(NamespaceTypeDecl)
		mut n := 0
		for *self.i < len(self.tokens); *self.i++ {
			mut token := self.tokens[*self.i]
			if n%2 == 0 {
				if token.Id != TokenId.Ident {
					self.pushErr(token, LogMsg.InvalidSyntax)
					self.pushSuggestion(LogMsg.ExpectedIdentifier)
				}
				nst.Idents = append(nst.Idents, token)
			} else if token.Id != TokenId.DblColon {
				break
			}
			n++
		}

		// Remove selected identifier token.
		nst.Idents = nst.Idents[:len(nst.Idents)-1]

		*self.i-- // Set offset to last identifier.
		nst.Kind = self.buildIdent()
		t.Kind = nst
		ret t
	}

	unsafe fn buildGenerics(mut self): []&TypeDecl {
		if *self.i >= len(self.tokens) {
			ret nil
		}
		token := self.tokens[*self.i]
		if token.Id != TokenId.LBracket {
			ret nil
		}

		mut parts := self.identGenerics()
		if len(parts) == 0 {
			self.pushErr(token, LogMsg.InvalidSyntax)
			ret nil
		}

		mut types := make([]&TypeDecl, 0, len(parts))
		for (_, mut part) in parts {
			mut j := 0
			mut t, _ := unsafe { self.p.buildType(part, &j, true) }
			if j < len(part) {
				self.pushErr(part[j], LogMsg.InvalidSyntax)
			}
			types = append(types, t)
		}
		ret types
	}

	unsafe fn identGenerics(mut self): [][]&Token {
		first := *self.i
		mut rangeN := 0
		for *self.i < len(self.tokens); *self.i++ {
			token := self.tokens[*self.i]
			match token.Id {
			| TokenId.LBracket:
				rangeN++
			| TokenId.RBracket:
				rangeN--
			}
			if rangeN == 0 {
				*self.i++ // Skip right bracket
				break
			}
		}
		mut tokens := self.tokens[first+1 : *self.i-1] // Take range of brackets.
		mut parts, errors := parts(tokens, TokenId.Comma, true)
		if self.err {
			self.p.errors = append(self.p.errors, errors...)
		}
		ret parts
	}

	unsafe fn buildIdent(mut self): &TypeDecl {
		mut next := *self.i+1 < len(self.tokens)
		if next && self.tokens[*self.i+1].Id == TokenId.DblColon {
			ret self.buildNamespace()
		}
		mut token := self.tokens[*self.i]
		mut it := &IdentTypeDecl{
			Token: token,
			Ident: token.Kind,
			Binded: false,
		}
		*self.i++
		mut itd := &TypeDecl{
			Token: token,
			Kind: it,
		}
		if !next || self.tokens[*self.i].Id != TokenId.Dot {
			it.Generics = self.buildGenerics()
			ret itd
		}
		if len(self.tokens)-*self.i == 1 {
			self.pushErr(token, LogMsg.InvalidSyntax)
			ret itd
		}
		*self.i++
		if self.tokens[*self.i].Id != TokenId.Ident {
			self.pushErr(self.tokens[*self.i-1], LogMsg.InvalidSyntax)
			ret itd
		}
		mut sit := new(SubIdentTypeDecl)
		sit.Idents = append(sit.Idents, it)
		mut ident := self.buildIdent()
		match type ident.Kind {
		| &IdentTypeDecl:
			sit.Idents = append(sit.Idents, (&IdentTypeDecl)(ident.Kind))
		| &SubIdentTypeDecl:
			sit.Idents = append(sit.Idents, (&SubIdentTypeDecl)(ident.Kind).Idents...)
		}
		ret &TypeDecl{
			Token: token,
			Kind: sit,
		}
	}

	unsafe fn buildCppLink(mut self): &TypeDecl {
		if *self.i+1 >= len(self.tokens) || self.tokens[*self.i+1].Id != TokenId.Dot {
			self.pushErr(self.tokens[*self.i], LogMsg.InvalidSyntax)
			ret nil
		}
		*self.i += 2 // Skip the bind keyword and dot token.
		mut t := self.buildIdent()
		(&IdentTypeDecl)(t.Kind).Binded = true
		ret t
	}

	unsafe fn buildFn(mut self): &TypeDecl {
		mut token := self.tokens[*self.i]
		mut f := self.p.buildFnPrototype(self.tokens, *self.i, false)
		if f == nil {
			ret nil
		}
		if !f.IsAnon() {
			self.pushErr(f.Token, LogMsg.InvalidSyntax)
		}
		ret &TypeDecl{
			Token: token,
			Kind: f,
		}
	}

	unsafe fn buildPtr(mut self): &TypeDecl {
		mut token := self.tokens[*self.i]
		if *self.i+1 >= len(self.tokens) {
			self.pushErr(token, LogMsg.InvalidSyntax)
			ret nil
		}

		*self.i++
		if self.tokens[*self.i].Id == TokenId.Unsafe {
			*self.i++
			ret &TypeDecl{
				Token: token,
				Kind: &PtrTypeDecl{
					Elem: nil, // Set Elem as nil for unsafe pointer (*unsafe) type. 
				},
			}
		}

		mut elem := self.step()
		if elem == nil {
			ret nil
		}

		ret &TypeDecl{
			Token: token,
			Kind: &PtrTypeDecl{
				Elem: elem,
			},
		}
	}

	unsafe fn buildSptr(mut self): &TypeDecl {
		mut token := self.tokens[*self.i]
		if *self.i+1 >= len(self.tokens) {
			self.pushErr(token, LogMsg.InvalidSyntax)
			ret nil
		}

		*self.i++
		mut elem := self.step()
		if elem == nil {
			ret nil
		}

		ret &TypeDecl{
			Token: token,
			Kind: &SptrTypeDecl{
				Elem: elem,
			},
		}
	}

	unsafe fn buildSlc(mut self): &TypeDecl {
		mut token := self.tokens[*self.i]
		*self.i++ // skip right bracket
		mut elem := self.step()
		if elem == nil {
			ret nil
		}
		ret &TypeDecl{
			Token: token,
			Kind: &SlcTypeDecl{
				Elem: elem,
			},
		}
	}

	unsafe fn buildArr(mut self): &TypeDecl {
		mut exprTokens := range(*self.i, TokenId.LBracket, TokenId.RBracket, self.tokens)
		if *self.i >= len(self.tokens) {
			self.pushErr(self.tokens[*self.i-1], LogMsg.MissingType)
			ret nil
		}

		mut elem := self.step()
		if elem == nil {
			ret nil
		}

		mut arrt := &ArrTypeDecl{
			Elem: elem,
		}

		mut token := exprTokens[0]
		if len(exprTokens) == 1 && token.Id == TokenId.TripleDot {
			// Ignore.
		} else {
			arrt.Size = self.p.buildExpr(exprTokens)
		}

		ret &TypeDecl{
			Token: token,
			Kind: arrt,
		}
	}

	unsafe fn buildMap(mut self): &TypeDecl {
		mut mapToken := self.tokens[*self.i]
		*self.i++ // Skip map token.
		if *self.i >= len(self.tokens) {
			self.pushErr(mapToken, LogMsg.MissingType)
			ret nil
		}

		// Get key type tokens without brackets.
		mut keyTokens := range(*self.i, TokenId.LBracket, TokenId.RBracket, self.tokens)
		if *self.i >= len(self.tokens) {
			self.pushErr(self.tokens[*self.i-1], LogMsg.MissingType)
			ret nil
		} else if len(keyTokens) == 0 {
			self.pushErr(mapToken, LogMsg.MissingType)
			ret nil
		}

		mut mapt := new(MapTypeDecl)
		mut j := 0
		mut keyt, mut ok := self.p.buildType(keyTokens, &j, self.err)
		if !ok {
			ret nil
		} else if j < len(keyTokens) {
			self.pushErr(keyTokens[j], LogMsg.InvalidSyntax)
		}
		mapt.Key = keyt

		mut valt, ok := self.p.buildType(self.tokens, self.i, self.err)
		if !ok {
			ret nil
		}
		mapt.Val = valt
		ret &TypeDecl{
			Token: mapToken,
			Kind: mapt,
		}
	}

	unsafe fn buildEnumerable(mut self): &TypeDecl {
		mut token := self.tokens[*self.i]
		if *self.i+2 >= len(self.tokens) {
			self.pushErr(token, LogMsg.InvalidSyntax)
			ret nil
		}
		*self.i++
		token = self.tokens[*self.i]
		if token.Id == TokenId.RBracket {
			ret self.buildSlc()
		}
		*self.i-- // Point to left bracket.
		ret self.buildArr()
	}

	unsafe fn step(mut self): &TypeDecl {
		token := self.tokens[*self.i]
		match token.Id {
		| TokenId.Ident:
			ret self.buildIdent()
		| TokenId.Cpp:
			ret self.buildCppLink()
		| TokenId.Unsafe:
			if len(self.tokens)-*self.i > 1 && self.tokens[*self.i+1].Id == TokenId.Fn {
				// unsafe function
				*self.i++ // point to function keyword
				mut f := self.buildFn()
				if f != nil {
					(&ast::FnDecl)(f.Kind).Unsafety = true
				}
				ret f
			}
		| TokenId.Fn:
			ret self.buildFn()
		| TokenId.Star:
			ret self.buildPtr()
		| TokenId.Amper:
			ret self.buildSptr()
		| TokenId.DblAmper:
			ret &TypeDecl{
				Kind: &SptrTypeDecl{
					Elem: self.buildSptr(),
				},
			}
		| TokenId.LBracket:
			ret self.buildEnumerable()
		| TokenId.Map:
			ret self.buildMap()
		}
		*self.i++
		self.pushErr(token, LogMsg.InvalidSyntax)
		ret nil
	}

	// Builds type.
	// Returns void if error occurs.
	unsafe fn build(mut self): (&TypeDecl, ok: bool) {
		mut root := self.step()
		if root == nil {
			ret buildVoidType(), false
		}
		ret root, true
	}
}