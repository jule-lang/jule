// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "std/internal/fmt"
use "std/unsafe"

enum status: byte {
	Na: 0x0,
	Skip: 0x1 << 0,
	Fail: 0x1 << 1,
}

// A test utility also used by the Jule runtime.
// It provides functionalities that facilitate the
// management and development of tests.
struct T {
	mut s: status
}

impl T {
	// Used by runtime.
	// Reset all data.
	fn reset(self) {
		self.s = status.Na
	}

	// Wrapper for internal logs.
	fn println(self, msg: str) {
		out("    ")
		outln(msg)
	}

	// Fails test.
	// Does not breaks scope execution.
	fn Fail(self) {
		if self.s == status.Skip {
			panic("testing: T.fail: failed test that already skipped")
		}
		self.s = status.Fail
	}

	// Reports whether test is failed.
	fn Failed(self): bool {
		ret self.s == status.Fail
	}

	// Skip test.
	// Does not breaks scope execution.
	fn Skip(self) {
		if self.s == status.Skip {
			panic("testing: T.skip: skipped test that already failed")
		}
		self.s = status.Skip
	}

	// Reports whether test is skipped.
	fn Skipped(self): bool {
		ret self.s == status.Skip
	}

	// Set status of test as failure if expression is evaluated false at runtime.
	fn Assert(self, expr: bool, message: str): bool {
		if !expr {
			self.println(message)
			self.Fail()
		}
		ret expr
	}

	// Set status of test as failure and print message by formatting.
	// Prints new-line after formatted text.
	// Uses std::fmt internally.
	fn Errorf(self, fmt: str, args: ...any) {
		self.println(unsafe::BytesStr(fmt::Format(fmt, args...)))
		self.Fail()
	}
}