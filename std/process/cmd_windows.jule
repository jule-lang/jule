// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use integ for std::jule::integrated

cpp use "<fcntl.h>"
cpp use "<unistd.h>"

cpp type HANDLE: *unsafe

cpp let P_NOWAIT: int

cpp fn _wspawnvp(mode: int, file: *integ::Wchar, args: **integ::Wchar): int
cpp fn _wspawnvpe(mode: int, file: *integ::Wchar, args: **integ::Wchar, env: **integ::Wchar): int
cpp unsafe fn _cwait(mut status: *integ::Int, pid: int, action: int): int

fn ustr_slice_to_wcstr_slice(us: [][]u16): []*integ::Wchar {
    let mut wcs = make([]*integ::Wchar, 0, us.len + 1)
    for i in us {
        wcs = append(wcs, unsafe { (*integ::Wchar)(&us[i][0]) })
    }
    wcs = append(wcs, nil)
    ret wcs
}

fn str_slice_to_ustr_slice(s: []str): [][]u16 {
    let mut us = make([][]u16, 0, s.len)
    for _, p in s {
        us = append(us, integ::utf16_from_str(p))
    }
    ret us
}

fn execvp(file: str, argv: []str): int {
    let utf16_file = integ::utf16_from_str(file)
    let ucargv = str_slice_to_ustr_slice(argv)
    let cargv = ustr_slice_to_wcstr_slice(ucargv)
    ret cpp._wspawnvp(cpp.P_NOWAIT, unsafe { (*integ::Wchar)(&utf16_file[0]) }, &cargv[0])
}

fn execve(file: str, argv: []str, env: []str): int {
    let ucargv = str_slice_to_ustr_slice(argv)
    let cargv = ustr_slice_to_wcstr_slice(ucargv)
    let ucenv = str_slice_to_ustr_slice(env)
    let cenv = ustr_slice_to_wcstr_slice(ucenv)
    let utf16_file = integ::utf16_from_str(file)
    ret cpp._wspawnvpe(cpp.P_NOWAIT, unsafe { (*integ::Wchar)(&utf16_file[0]) }, &cargv[0], &cenv[0])
}

impl Cmd {
    fn __spawn(self)!: int {
        let pid = if self.env == nil {
            execvp(self.path, self.args)
        } else {
            execve(self.path, self.args, self.env)
        }
        if pid == -1 {
            error(get_last_process_error())
        }
        let status = 0
        if unsafe { cpp._cwait((*integ::Int)(&status), pid, 0) } == -1 {
            error(get_last_process_error())
        }
        ret status
    }
}
