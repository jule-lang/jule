// Copyright 2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Single bit.
type bit: byte

// In internal order, bits are stored in reversed order for performance purposes.
type bits: []bit

const _INT_SIZE = 32 << (^uint(0) >> 63)

fn u64FromBits(b: bits): u64 {
    mut u := u64(0)
    for i, bit in b {
        u |= u64(bit) << i
    }
    ret u
}

// Add one to bits.
// Equals to x++ operator.
fn addOne(mut &b: bits): (carry: bit) {
    for i, x in b {
        if x == 0 {
            b[i] = 1
            ret 0b0
        }
        b[i] = 0
    }
    ret 0b1
}

// Subrract one from bits.
// Equals to x-- operator.
fn subOne(mut &b: bits) {
    mut j := 0
    for i, x in b {
        if x == 0b1 {
            b[i] = 0b0
            j = i
            break
        }
        b[i] = 0
    }
    for i in b[:j] {
        b[i] = 0b1
    }
}

fn reverse(mut &b: bits) {
    mut i := 0
    for i < len(b)>>1; i++ {
        b[i], b[len(b)-1-i] = b[len(b)-1-i], b[i]
    }
}

// Add bits with size responsive.
fn addRes(mut &x: bits, &y: bits) {
    mut carry := bit(0)
    match {
    | len(y) == 1:
        carry = addOne(x)
    | len(x) == len(y):
        carry = addFast(x, y)
    | len(x) > len(y):
        carry = addRfast(x, y)
    |:
        x, carry = add(y, x)
    }
    if carry == 0b1 {
        x = append(x, 0b1)
    }
}

// Subtract bits with size responsive.
fn subRes(mut &x: bits, y: bits) {
    mut carry := bit(0)
    match {
    | len(y) == 1:
        twosComplement(x)
        carry = addOne(x)
    | len(x) == len(y):
        twosComplement(x)
        carry = addFast(x, y)
    | len(x) > len(y):
        twosComplement(x)
        carry = addRfast(x, y)
    |:
        mut xn := make(bits, len(y))
        _ = copy(xn, x)
        x = xn
        twosComplement(x)
        carry = addFast(x, y)
    }
    if carry == 0b0 {
        twosComplement(x)
    }
}

// Bitwise or.
// a's bitsize should be have same or greather than b's bitsize.
fn or(mut &a: bits, &b: bits) {
    for i in b {
        a[i] |= b[i]
    }
}

// Bitwise xor.
// a's bitsize should be have same or greather than b's bitsize.
fn xor(mut &a: bits, &b: bits) {
    for i in b {
        a[i] ^= b[i]
    }
}

// Bitwise and.
// a's bitsize should be have same or greather than b's bitsize.
fn and(mut &a: bits, &b: bits) {
    for i in b {
        a[i] &= b[i]
    }
    // Clean trailing bits.
    if len(a) > len(b) {
        mut trail := a[len(b):]
        for i in trail {
            trail[i] = 0b0
        }
    }
}

// Update bits by 1's complement.
fn onesComplement(mut &b: bits) {
    for i, x in b {
        b[i] = ^x & 0b1
    }
}

// Update bits by 2's complement.
fn twosComplement(mut &b: bits) {
    onesComplement(b)
    _ = addOne(b)
}

// Addition operation on operand bits.
// Returns bits and carry.
// Carry is guaranteed to be 0b0 or 0b1.
// Does not allocates new bits for result, updates elements of x.
// x and y should be have same bitsize.
fn addFast(mut &x: bits, &y: bits): (carry: bit) {
    for i, xbit in x {
        ybit := y[i]
        x[i] = xbit ^ ybit ^ carry
        carry = (xbit & ybit) | (xbit & carry) | (ybit & carry)
    }
    ret
}

// Addition operation on operand bits.
// Returns bits and carry.
// Carry is guaranteed to be 0b0 or 0b1.
// Does not allocates new bits for result, updates elements of x.
// y implies imaginary zero-bits for missing part if y.bit-size < x.bit-size.
fn addRfast(mut &x: bits, &y: bits): (carry: bit) {
    for i, xbit in x {
        mut ybit := bit(0b0)
        if i < len(y) {
            ybit = y[i]
        }
        x[i] = xbit ^ ybit ^ carry
        carry = (xbit & ybit) | (xbit & carry) | (ybit & carry)
    }
    ret
}

// Addition operation on operand bits.
// Returns bits and carry.
// Carry is guaranteed to be 0b0 or 0b1.
// y implies imaginary zero-bits for missing part if y.bit-size < x.bit-size.
fn add(&x: bits, &y: bits): (r: bits, carry: bit) {
    r = make(bits, 0, len(x))
    for i, xbit in x {
        mut ybit := bit(0b0)
        if i < len(y) {
            ybit = y[i]
        }
        r = append(r, xbit ^ ybit ^ carry)
        carry = (xbit & ybit) | (xbit & carry) | (ybit & carry)
    }
    ret
}

// Eliminates initial-zeros, normalizes bits.
fn fit(mut &b: bits) {
    mut i := len(b) - 1
    for i >= 0; i-- {
        if b[i] != 0b0 {
            break
        }
    }
    b = b[:i+1]
}

// Same as fit, but designed for normal order bits.
fn fitRev(mut &b: bits) {
    for i, bit in b {
        if bit != 0b0 {
            b = b[i:]
            break
        }
    }
}

// Compares bits. Bits should be normalized.
// Returns +1 if x > y.
// Returns 0 if x == y.
// Returns -1 if x < y.
fn cmp(x: bits, &y: bits): int {
    match {
    | len(x) < len(y):
        ret -1
    | len(x) > len(y):
        ret +1
    }
    mut i := len(x) - 1
    for i >= 0; i-- {
        xbit := x[i]
        ybit := y[i]
        match {
        | xbit > ybit:
            ret +1
        | xbit < ybit:
            ret -1
        }
    }
    ret 0
}

// Same as cmp, but designed for normal order bits.
fn cmpRev(mut &x: bits, mut &y: bits): int {
    match {
    | len(x) < len(y):
        ret -1
    | len(x) > len(y):
        ret +1
    }
    for i in x {
        xbit := x[i]
        ybit := y[i]
        match {
        | xbit > ybit:
            ret +1
        | xbit < ybit:
            ret -1
        }
    }
    ret 0
}

// Reports whether number is odd that represented by bits.
fn isOdd(&b: bits): bool {
    ret len(b) > 0 && b[0] == 0b1
}

// Reports whether number is even that represented by bits.
fn isEven(&b: bits): bool {
    ret len(b) > 0 && b[0] == 0b0
}

// Basic multiplication algorithm for bits.
fn basicMul(mut &x: bits, mut y: bits): bits {
    if len(y) == 1 {
        ret x
    }
    let mut r: bits = nil
    mut m := make(bits, len(x), len(x) << 1)
    _ = copy(m, x)
    for len(y) > 0 {
        if isOdd(y) {
            addRes(r, m)
        }
        m = append(m[:1], m...)
        m[0] = 0b0
        y = y[1:]
    }
    ret r
}

fn karatsubaAdd(mut x: bits, mut y: bits): bits {
    reverse(x)
    reverse(y)
    defer {
        reverse(x)
        reverse(y)
    }

    mut carry := bit(0)
    let mut r: bits = nil
    match {
    | len(x) == len(y):
        r, carry = add(x, y)
    | len(x) > len(y):
        r, carry = add(x, y)
    |:
        r, carry = add(y, x)
    }
    if carry == 0b1 {
        r = append(r, 0b1)
    }
    reverse(r)
    ret r
}

fn cloneBits(x: bits): bits {
    ret append(make(bits, 0, len(x)), x...)
}

fn karatsubaSub(mut x: bits, mut y: bits): bits {
    x = cloneBits(x)
    reverse(x)
    reverse(y)

    subRes(x, y)
    reverse(x)
    reverse(y)
    ret x
}

fn karatsubaShl(mut s: bits, n: int): bits {
    ret append(s, make(bits, n)...)
}

// This threshold is represents number of bits small numbers for karatsuba algorithm.
// If the y operand's bitlength <= threshold, use basic multiplication algorithm.
const karatsubaThreshold = 1 << 5 + 8

fn karatsuba(mut x: bits, mut y: bits): bits {
    n := max(len(x), len(y))
    if n == 1 {
        if len(x) == 0 || len(y) == 0 || x[0] == 0b0 || y[0] == 0b0 {
            ret nil
        }
        ret [1]
    }

    if len(y) < karatsubaThreshold {
        reverse(x)
        reverse(y)
        mut r := basicMul(x, y)
        reverse(x)
        reverse(y)
        reverse(r)
        ret r
    }

    m := n >> 1
    mut xl := x[:m]
    mut xr := x[m:]
    mut yl := y[:m]
    mut yr := y[m:]

    mut p1 := karatsuba(xl, yl)
    mut p2 := karatsuba(xr, yr)
    mut p3 := karatsuba(karatsubaAdd(xl, xr), karatsubaAdd(yl, yr))

    mut c2 := karatsubaShl(karatsubaSub(p3, karatsubaAdd(p1, p2)), n - m)
    mut c1 := karatsubaShl(p1, (n - m) << 1)
    ret karatsubaAdd(karatsubaAdd(c1, c2), p2)
}

// Recursion division algorithm. It will update left operand if necessary.
// Uses bit shifting strategy.
// Returns quotient.
//
// The Theory and How it Fast?
//
//     In the worst case, the algorithm performs three allocations. One of them is for x.
//     Because x is used directly and its value is changed. The other one is q, that is,
//     quotient allocation. This is also used directly and its value changes. The algorithm
//     only has these potential allocations before it is used, it will  not result in any
//     additional allocations. The third allocation is s which is stores sum of
//     quetient parts.
//
//     s will be used to collect all quotient parts. It must be allocated beforehand
//     and must be the same as the bit length of x and initialized with zero.
//     Thus, the carry check can be skipped because there will never be an overflow
//     and the total quotient cannot be greater than the dividend. When the algorithm
//     is completed, s will store the quotient to be calculated.
//
//     q will be used to find the y<<n which is closest to x at each step.
//     Therefore it will always be equal or greater than x. q is evaluated to be
//     equal to x in the worst case. Therefore, before q is used in the algorithm,
//     it must be allocated the length of x. Its value must be initialized with y.
//     y will always be less than x. Therefore, there will be a difference in the
//     number of bits between them. The q << n operation must be applied as much as
//     the difference in the remaining number of bits. This will produce y<<n which
//     is greater than or equal to the calculation closest to x.
//
//         For example:
//           x = 1101100110000100101001001111101111000110101
//           y = 1011001
//           q = 1011001000000000000000000000000000000000000
//
//         For example:
//           x = 1101100110000100101001001111101111000110101
//           y = 111
//           q = 1110000000000000000000000000000000000000000
//
//     To calculate s correctly, the quotient must be summed to the result at
//     each step. So s += quotient. q is not exactly a quotient. Let's say a y<<36.
//     q refers to the closest number that can be reached by shifting x from y.
//     So a q with y<<36 means 36 shifts have been made. But this is not the count of
//     additions of y with itself 36 times, so it cannot be used as a quotient.
//     To calculate quotient correctly, 1<<n must be calculated using the number
//     of shifts of q. This is exactly y<<n equals to how many y's are added together.
//     So 1<<36 is equals to actual quotient of q which is should add to s.
//
//  Proof and Example
//
//     Our case: 7473866464821 % 89;
//     Which is:
//         x = 7473866464821         | 1101100110000100101001001111101111000110101
//         y = 89                    | 1011001
//         q = 6116033429504 (y<<36) | 1011001000000000000000000000000000000000000
//         s = 0                     | 0000000000000000000000000000000000000000000
//
//     q was calculated as described and resulted in exactly y<<36.
//     This supports the claim that for this case this calculation will always
//     produce y<<n greater than or exactly closest to x.
//
//     The theory then claims that q can be reduced in the same way after each step,
//     which will be faster because there will often be right shifts are more close
//     to next closest y<<n to x than starting to compute from y<<1 again.
//
//       Execution:
//
//           1.       1<<36
//           2.       1<<33
//           3.       1<<32
//           4.       1<<31
//           5.       1<<27
//           6.       1<<26
//           7.       1<<24
//           8.       1<<22
//           9.       1<<20
//           10.      1<<19
//           11.      1<<18
//           12.      1<<14
//           13.      1<<13
//           14.      1<<12
//           15.      1<<11
//           16.      1<<10
//           17.      1<<9
//           18.      1<<5
//           19.      1<<3
//           20.      1<<2
//           21.      1<<1
//                 +
//                 -----------
//                 83976027694
//
fn recursiveDiv(mut &x: bits, mut &y: bits, mut &s: bits, mut &q: bits) {
    match cmp(x, y) {
    | -1:
        ret
    | 0:
        addOne(s)
        ret
    }
    for cmp(q, x) == +1 {
        q = q[1:]
    }
    if len(q) == len(y) {
        addOne(s)
        ret
    }
    subRes(x, q)
    fit(x)
    mut sq := q[:len(q)-len(y)+1]
    mut &last := unsafe { *(&sq[len(sq)-1]) }
    old := last
    last = 0b1
    addRfast(s, sq)
    last = old
    recursiveDiv(x, y, s, q)
}

// Recursion modulo algorithm. It will update left operand if necessary.
// Uses bit shifting strategy.
// Returns remainder.
//
// The Theory and How it Fast?
//
//     In the worst case, the algorithm performs two allocations. One of them is for x.
//     Because x is used directly and its value is changed. The other one is q, that is,
//     quotient allocation. This is also used directly and its value changes. The algorithm
//     only has these potential allocations before it is used, it will  not result in any
//     additional allocations.
//
//     q will be used to find the y<<n which is closest to x at each step.
//     Therefore it will always be equal or greater than x. q is evaluated to be
//     equal to x in the worst case. Therefore, before q is used in the algorithm,
//     it must be allocated the length of x. Its value must be initialized with y.
//     y will always be less than x. Therefore, there will be a difference in the
//     number of bits between them. The q << n operation must be applied as much as
//     the difference in the remaining number of bits. This will produce y<<n which
//     is greater than or equal to the calculation closest to x.
//
//         For example:
//           x = 1101100110000100101001001111101111000110101
//           y = 1011001
//           q = 1011001000000000000000000000000000000000000
//
//         For example:
//           x = 1101100110000100101001001111101111000110101
//           y = 111
//           q = 1110000000000000000000000000000000000000000
//
//     At each step, the algorithm first makes a comparison for x and y.
//     If they are equal, it returns zero since there will be no remainder.
//     In case x < y, it returns x since the remainder will be x.
//
//     Then q is calculated for the nearest y<<n that is less than or equal to x.
//     Since q will always be greater than or equal to x, only the shifted bits should be
//     shifted to the right until this condition is met. This is faster than starting from y<<1
//     and going all the way to y<<n at each step. Usually, the peak is at a number close to
//     the bits of the divisor (x). Therefore, it is wiser to drop back from the total number of bits.
//
//     Then q will be subtracted from x, thus reducing the value of x by often more than x/2.
//     Then the process is repeated. q is left untouched because it satisfies the condition
//     that it will always be greater than or equal to x. So q can always be shifted to the
//     right to reach a nearby number.
//
//  Proof and Example
//
//     Our case: 7473866464821 % 89;
//     Which is:
//         x = 7473866464821         | 1101100110000100101001001111101111000110101
//         y = 89                    | 1011001
//         q = 6116033429504 (y<<36) | 1011001000000000000000000000000000000000000
//
//     q was calculated as described and resulted in exactly y<<36.
//     This supports the claim that for this case this calculation will always
//     produce y<<n greater than or exactly closest to x.
//
//     The theory then claims that q can be reduced in the same way after each step,
//     which will be faster because there will often be right shifts are more close
//     to next closest y<<n to x than starting to compute from y<<1 again.
//
//       Execution:
//
//                    x          q
//           1. 7473866464821 - y<<36
//           2. 1357833035317 - y<<33
//           3.  593328856629 - y<<32
//           4.  211076767285 - y<<31
//           5.   19950722613 - y<<27
//           6.    8005344821 - y<<26
//           7.    2032655925 - y<<24
//           8.     539483701 - y<<22
//           9.     166190645 - y<<20
//           10.     72867381 - y<<19
//           11.     26205749 - y<<18
//           12.      2874933 - y<<14
//           13.      1416757 - y<<13
//           14.       687669 - y<<12
//           15.       323125 - y<<11
//           16.       140853 - y<<10
//           17.        49717 - y<<9
//           18.         4149 - y<<5
//           19.         1301 - y<<3
//           20.          589 - y<<2
//           21.          233 - y<<1
//                         55 = remainder, x < y
//
fn recursiveMod(mut &x: bits, &y: bits, mut &q: bits): bits {
    match cmp(x, y) {
    | 0:
        ret nil
    | -1:
        ret x
    }
    for cmp(q, x) == +1 {
        q = q[1:]
    }
    subRes(x, q)
    fit(x)
    ret recursiveMod(x, y, q)
}