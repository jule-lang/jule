// Copyright 2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// An arbitrary-precision natural number.
#derive Clone
struct Nat {
    mut bits: bits
}

impl Nat {
    // Returns Nat that represents zero.
    static fn zero(): Nat {
        ret Nat{}
    }

    // Returns Nat that represents one.
    static fn one(): Nat {
        ret Nat{
            bits: [1],
        }
    }

    // Returns Nat that initialized by integer value.
    // T can only be signed or unsigned integer types.
    pub static fn new[T](mut i: T): Nat {
        match type T {
        | Nat:
            ret i
        }
        let mut nat = Nat.zero()
        match type T {
        | i8:
            nat.bits = make(bits, 1 << 3 - 1)
        | u8:
            nat.bits = make(bits, 1 << 3)
        | i16:
            nat.bits = make(bits, 1 << 4 - 1)
        | u16:
            nat.bits = make(bits, 1 << 4)
        | i32:
            nat.bits = make(bits, 1 << 5 - 1)
        | u32:
            nat.bits = make(bits, 1 << 5)
        | i64:
            nat.bits = make(bits, 1 << 6 - 1)
        | u64:
            nat.bits = make(bits, 1 << 6)
        | int:
            nat.bits = make(bits, INT_SIZE - 1)
        | uint:
            nat.bits = make(bits, INT_SIZE)
        |:
            panic("std::math::big: Nat.new[T]: T is should be signed or unsigned integer type")
        }
        if i < 0 {
            i = -i
        }
        for j in nat.bits {
            nat.bits[j] = bit((i >> j) & 0b1)
        }
        nat.fit()
        ret nat
    }

    // Parse Nat from string. Fmt is the format of string.
    // Results with exceptional if bits are not in the format or empty.
    // Results with exceptional if fmt is not valid.
    // Exceptional is always BigError.Format.
    //
    // Valid fmt values are;
    //   - 2 for binary.
    //   - 16 for hexadecimal.
    static fn parse(s: str, fmt: int)!: Nat {
        if s == "" {
            error(BigError.Format)
        }
        let mut r = Nat{}
        match fmt {
        | 2:
            r.bits = parse_binary(s) else { error(error) }
        | 16:
            r.bits = parse_hex(s) else { error(error) }
        |:
            error(BigError.Format)
        }
        ret r
    }

    // Returns count of bits.
    // Return value also means the minimum number of bits that can represent the integer.
    fn len(self): int {
        ret self.bits.len
    }

    // Eliminates initial-zeros, normalizes bits.
    fn fit(mut self) {
        fit(self.bits)
    }

    // Adds two Nat and returns result.
    pub fn add(self, y: Nat): Nat {
        let mut r = self
        r += y
        ret r
    }

    // Adds Nat.
    pub fn add_assign(mut self, y: Nat) {
        if y.len() == 0 {
            ret
        }
        self.bits = clone(self.bits)
        add_res(self.bits, y.bits)
    }

    // Subtracts two Nat and returns result.
    pub fn sub(self, y: Nat): Nat {
        let mut r = self
        r -= y
        ret r
    }

    // Subtracts Nat.
    pub fn sub_assign(mut self, y: Nat) {
        if y.len() == 0 {
            ret
        }
        self.bits = clone(self.bits)
        sub_res(self.bits, y.bits)
        self.fit()
    }

    // Multiplies two Nat and returns result.
    pub fn mul(self, y: Nat): Nat {
        let mut r = self
        r *= y
        ret r
    }

    // Multiplies Nat.
    pub fn mul_assign(mut self, y: Nat) {
        match y.len() {
        | 1:
            ret
        | 0:
            // Right operand is zero, result is always zero.
            self.bits = nil
            ret
        }
        if self.len() == 0 {
            // Right operand is zero, result is always zero.
            self.bits = nil
            ret
        }

        // The opeand y is small number.
        // Use basic algorithm instead of karatsuba.
        if y.len() < KARATSUBA_THRESHOLD {
            self.bits = basic_mul(self.bits, y.bits)
            self.fit()
            ret
        }

        // Make size of operands are equal if not.
        let mut xb = self.bits
        let mut yb = y.bits
        let n = max(xb.len, yb.len)
        if xb.len < n {
            let mut xbn = make(bits, n)
            _ = copy(xbn, xb)
            xb = xbn
        }
        if yb.len < n {
            let mut ybn = make(bits, n)
            _ = copy(ybn, yb)
            yb = ybn
        }

        // Reverse bits, karatsuba uses normal-order bits.
        reverse(xb)
        reverse(yb)

        let mut r = karatsuba(xb, yb)

        // Reverse bits to save internal order.
        if self.len() < n {
            reverse(self.bits)
        }
        if y.len() < n {
            reverse(y.bits)
        }
        reverse(r)

        self.bits = r
        self.fit()
    }

    // Divides two Nat and returns result.
    pub fn div(self, y: Nat): Nat {
        let mut r = self
        r /= y
        ret r
    }

    // Divides Nat.
    pub fn div_assign(mut self, y: Nat) {
        if self.len() == 0 {
            // Left operans is zero, remainder is always zero.
            ret
        }
        match y.len() {
        | 1:
            // Right operand is 1, quotient is always equals to left operand.
            ret
        | 0:
            panic("std::math::big: division by zero")
        }
        match self.cmp(y) {
        | -1:
            // Left operand is less than right oprand.
            // Quotient is always equals to zero.
            self.bits = nil
            ret
        | 0:
            // Left oprand and right operand are equal.
            // Quotient is always zero.
            self.bits = [1]
            ret
        }
        let mut xb = clone(self.bits)
        let mut q = make(bits, xb.len)
        _ = copy(q[q.len - y.bits.len:], y.bits)
        let mut s = make(bits, xb.len)
        recursive_div(xb, y.bits, s, q)
        self.bits = s
        self.fit()
    }

    // Modulo two Nat and returns result.
    pub fn mod(self, y: Nat): Nat {
        let mut r = self
        r %= y
        ret r
    }

    // Modulo Nat.
    pub fn mod_assign(mut self, y: Nat) {
        if self.len() == 0 {
            // Left operans is zero, remainder is always zero.
            ret
        }
        match {
        | y.len() == 1:
            // Right operand is 1, remainder is always zero.
            self.bits = nil
            ret
        | y.len() == 2 && y.bits[0] == 0b0:
            // Right operand is 2.
            // If left opeand is even, remainder is always zero.
            // If left operand is odd, remainder is always one.
            if self.even() {
               self.bits = nil
            } else {
               self.bits = [1]
            }
            ret
        | y.len() == 0:
            panic("std::math::big: division by zero")
        }
        match self.cmp(y) {
        | -1:
            // Left operand less than right operand.
            // Remainder always equals to left operand.
            ret
        | 0:
            // Left oprand and right operand are equal.
            // Remainder is always zero.
            self.bits = nil
            ret
        }
        let mut xb = clone(self.bits)
        let mut q = make(bits, xb.len)
        _ = copy(q[q.len - y.bits.len:], y.bits)
        self.bits = recursive_mod(xb, y.bits, q)
        self.fit()
    }

    // Bitwise left shift.
    pub fn shl(self, y: uint): Nat {
        let mut r = self
        r <<= y
        ret r
    }

    // Bitwise left shift for assignment.
    pub fn shl_assign(mut self, y: uint) {
        if y > 0 && self.len() > 0 {
            let mut xbits = make(bits, self.len() + int(y))
            _ = copy(xbits[y:], self.bits)
            self.bits = xbits
        }
    }

    // Bitwise right shift.
    pub fn shr(self, y: uint): Nat {
        let mut r = self
        r >>= y
        ret r
    }

    // Bitwise right shift for assignment.
    pub fn shr_assign(mut self, y: uint) {
        if y == 0 {
            ret
        }
        if y > uint(self.len()) {
            self.bits = nil
            ret
        }
        self.bits = self.bits[y:]
    }

    // Bitwise or.
    pub fn bit_or(self, y: Nat): Nat {
        let mut r = self
        r |= y
        ret r
    }

    // Bitwise or for assignment.
    pub fn bit_or_assign(mut self, y: Nat) {
        if self.len() < y.len() {
            self = y | self
            ret
        }
        self.bits = clone(self.bits)
        or(self.bits, y.bits)
        self.fit()
    }

    // Bitwise and.
    pub fn bit_and(self, y: Nat): Nat {
        let mut r = self
        r &= y
        ret r
    }

    // Bitwise and for assignment.
    pub fn bit_and_assign(mut self, y: Nat) {
        if self.len() < y.len() {
            self = y & self
            ret
        }
        self.bits = clone(self.bits)
        and(self.bits, y.bits)
        self.fit()
    }

    // Bitwise and.
    pub fn bit_xor(self, y: Nat): Nat {
        let mut r = self
        r ^= y
        ret r
    }

    // Bitwise xor for assignment.
    pub fn bit_xor_assign(mut self, y: Nat) {
        if self.len() < y.len() {
            self = y ^ self
            ret
        }
        self.bits = clone(self.bits)
        xor(self.bits, y.bits)
        self.fit()
    }

    // Compares bits.
    // Returns +1 if self > y.
    // Returns 0 if self == y.
    // Returns -1 if self < y.
    fn cmp(self, y: Nat): int {
        ret cmp(self.bits, y.bits)
    }

    // Reports whether integer less than other.
    pub fn lt(self, y: Nat): bool {
        ret self.cmp(y) == -1
    }

    // Reports whether integer less than or equals to other.
    pub fn lt_eq(self, y: Nat): bool {
        ret self.cmp(y) < +1
    }

    // Reports whether integer greater than other.
    pub fn gt(self, y: Nat): bool {
        ret self.cmp(y) == +1
    }

    // Reports whether integer greater than or equals to other.
    pub fn gt_eq(self, y: Nat): bool {
        ret self.cmp(y) > -1
    }

    // Reports whether bits are equals.
    pub fn eq(self, y: Nat): bool {
        ret self.cmp(y) == 0
    }

    // Reports whether number is odd.
    pub fn odd(self): bool {
        ret is_odd(self.bits)
    }

    // Reports whether number is even.
    pub fn even(self): bool {
        ret is_even(self.bits)
    }

    // Returns bit by index.
    // The index zero means first bit at right.
    pub fn bit(self, i: int): int {
        if i < 0 {
            panic("std::math::big: negative bit index")
        }
        if i >= self.len() {
            ret 0
        }
        ret int(self.bits[i])
    }

    // Format number into string. Fmt is the format of number.
    // Results with exceptional if fmt is not valid.
    // Exceptional is always BigError.Format.
    //
    // Valid fmt values are;
    //   - 2 for binary.
    //   - 8 for octal.
    ///  - 10 for decimal.
    ///  - 16 for hexadecimal.
    pub fn format(self, fmt: int)!: str {
        match fmt {
        | 2:
            ret str(format_binary(self.bits))
        | 8:
            ret str(format_octal(self.bits))
        | 10:
            ret str(format_decimal(self.bits))
        | 16:
            ret str(format_hex(self.bits))
        |:
            error(BigError.Format)
        }
    }

    // Returns integer in u64.
    // Causes exception if nuber large than capacity of u64.
    // Exception is always equals to u64.MAX constant.
    fn to_u64(self)!: u64 {
        if self.len() > 64 {
            error(u64.MAX)
        }
        ret u64_from_bits(self.bits)
    }

    // Formats number with self.format(10) by default.
    pub fn to_str(self): str {
        ret self.format(10)!
    }
}
