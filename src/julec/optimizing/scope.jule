// Copyright 2023 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use env

use path for std::fs::path
use std::jule::build::{PATH_STDLIB}
use std::jule::constant::{Const}
use std::jule::sema::{
    Data,
    Scope,
    St,
    FnCallExprModel,
    Conditional,
    If,
    Case,
    Match,
}

// Scope optimizer that applies target-independent optimizations.
struct ScopeOptimizer {
    i:     int
    scope: &Scope
}

impl ScopeOptimizer {
    pub static fn new(mut scope: &Scope): &ScopeOptimizer {
        ret &ScopeOptimizer{
            scope: scope,
        }
    }

    fn set_current_stmt(mut self, mut stmt: any) {
        self.scope.stmts[self.i] = stmt
    }

    fn current_stmt(mut self): any {
        ret self.scope.stmts[self.i]
    }

    fn optimize_data(mut self, mut d: &Data) {
        match type d.model {
        | &FnCallExprModel:
            let mut m = (&FnCallExprModel)(d.model)
            if env::PRODUCTION {
                if !m.func.is_builtin() &&
                    is_std_package(m.func.decl.token.file.path(), "debug") {
                    self.scope.stmts[self.i] = nil
                }
            }
        }
    }

    fn optimize_conditional(mut self, mut c: &Conditional) {
        if !env::OPT_COND {
            ret
        }

        let mut const_case = false // Has cosntant true case.

        // Remove unnecessary trailing cases that comes after constant true case.
        for (i, mut elif) in c.elifs {
            if !is_constant_valid_conditional_case(elif) {
                continue
            }
            const_case = true
            c.elifs = c.elifs[:i+1]
            elif.expr = nil // Remove expression to remove eval overhead.
            c.default = nil // Remove default case, unreachable.
            break
        }

        // Remove unreachable cases.
        let mut i = &c.elifs[0]
        let end = &c.elifs[c.elifs.len-1]
        for i <= end; i++ {
            unsafe {
                if !is_unreachable_conditional_case(*i) {
                    i++
                    continue
                }
                *i = nil
            }
        }

        if c.elifs.len == 0 {
            ret
        }

        // Skip one-case checking if const-case is not exist.
        if !const_case {
            ret
        }

        let mut only = -1
        for j, elif in c.elifs {
            if elif != nil {
                if only != -1 {
                    // Break checking, there is more than one case.
                    ret
                }
                only = j
            }
        }

        // Here is one case.
        if only != -1 {
            self.set_current_stmt(c.elifs[only].scope)
        }
    }

    fn optimize_match(mut self, mut m: &Match) {
        if !env::OPT_COND {
            ret
        }

        let mut const_case = false // Has cosntant true case.

        // Remove unnecessary trailing cases that comes after constant true case.
        for (i, mut case) in m.cases {
            if !is_constant_valid_match_case(case) {
                continue
            }
            const_case = true
            m.cases = m.cases[:i+1]
            case.exprs = nil // Remove expressions to remove eval overhead.
            m.default = nil  // Remove default case, unreachable.
            break
        }

        if m.cases.len == 0 {
            ret
        }

        // Remove unreachable cases.
        let mut i = &m.cases[0]
        let end = &m.cases[m.cases.len-1]
        for i <= end; i++ {
            unsafe {
                if !is_unreachable_match_case(*i) {
                    i++
                    continue
                }
                *i = nil
            }
        }

        // Skip one-case checking if const-case is not exist.
        if !const_case {
            ret
        }

        let mut only = -1
        for j, case in m.cases {
            if case != nil {
                if only != -1 {
                    // Break checking, there is more than one case.
                    ret
                }
                only = j
            }
        }

        // Here is one case.
        if only != -1 {
            self.set_current_stmt(m.cases[only].scope)
        }
    }

    fn optimize_stmt(mut self, mut stmt: St) {
        match type stmt {
        | &Scope:       self.optimize_child((&Scope)(stmt))
        | &Data:        self.optimize_data((&Data)(stmt))
        | &Conditional: self.optimize_conditional((&Conditional)(stmt))
        | &Match:       self.optimize_match((&Match)(stmt))
        }
    }

    fn optimize_child(mut self, mut child: &Scope) {
        let mut so = ScopeOptimizer.new(child)
        so.optimize()
    }

    // Optimizes scope by enabled optimizations.
    pub fn optimize(mut self) {
        self.i = 0
        for self.i < self.scope.stmts.len; self.i++ {
            self.optimize_stmt(self.current_stmt())
        }
    }
}

fn is_std_package(f: str, p: str): bool {
    ret f.has_prefix(path::join(PATH_STDLIB, p))
}

fn is_constant_valid_conditional_case(&i: &If): bool {
    match type i.expr {
    | &Const:
        let c = (&Const)(i.expr)
        ret c.is_bool() && c.read_bool()
    }

    ret false
}

fn is_constant_valid_match_case(&case: &Case): bool {
    for _, expr in case.exprs {
        if expr.is_const() && expr.constant.is_bool() && expr.constant.read_bool() {
            ret true
        }
    }

    ret false
}

fn is_unreachable_conditional_case(&i: &If): bool {
    match type i.expr {
    | &Const:
        let c = (&Const)(i.expr)
        ret c.is_bool() && !c.read_bool()
    }

    ret false
}

fn is_unreachable_match_case(&case: &Case): bool {
    for _, expr in case.exprs {
        if expr.is_const() && expr.constant.is_bool() && !expr.constant.read_bool() {
            ret true
        }
    }

    ret false
}
