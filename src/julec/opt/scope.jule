// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use env
use path for std::fs::path
use std::jule::build::{PathStdlib}
use std::jule::constant::{Const}
use std::jule::lex::{TokenKind}
use std::jule::sema::{
    Var,
    Data,
    Scope,
    Stmt,
    ExprModel,
    FnCallExprModel,
    Conditional,
    CastingExprModel,
    If,
    Else,
    Case,
    Match,
    RangeIter,
    WhileIter,
    InfIter,
    Assign,
    MultiAssign,
    RetSt,
    BinopExprModel,
    OperandExprModel,
    BuiltinAppendCallExprModel,
    SliceExprModel,
    StructSubIdentExprModel,
    SlicingExprModel,
    FallSt,
}
use strings for std::strings

// Scope optimizer that applies target-independent optimizations.
struct scopeOptimizer {
    i:     int
    scope: &Scope
}

impl scopeOptimizer {
    static fn new(mut scope: &Scope): &scopeOptimizer {
        ret &scopeOptimizer{
            scope: scope,
        }
    }

    fn setCurrentStmt(mut self, mut stmt: any) {
        self.scope.Stmts[self.i] = unsafe { *(*Stmt)(&stmt) }
    }

    fn optimizeData(mut self, mut d: &Data) {
        match type d.Model {
        | &FnCallExprModel:
            let mut m = (&FnCallExprModel)(d.Model)
            if env::Production {
                if !m.Func.IsBuiltin() &&
                    isStdPackage(m.Func.Decl.Token.File.Path, "debug") {
                    self.setCurrentStmt(nil)
                    ret
                }
            }
        }
        exprOptimizer.optimize(d.Model)
    }

    fn optimizeVar(mut self, mut v: &Var) {
        if v.Value != nil {
            exprOptimizer.optimize(v.Value.Data.Model)
        }
    }

    fn optimizeConditional(mut self, mut c: &Conditional) {
        for (_, mut elif) in c.Elifs {
            exprOptimizer.optimize(elif.Expr)
            self.optimizeChild(elif.Scope)
        }
        if c.Default != nil {
            self.optimizeChild(c.Default.Scope)
        }

        if !Cond {
            ret
        }

        let mut constCase = false // Has cosntant true case.

        // Remove unnecessary trailing cases that comes after constant true case.
        for (i, mut elif) in c.Elifs {
            if !isConstantValidConditionalCase(elif) {
                continue
            }
            constCase = true
            c.Elifs = c.Elifs[:i]
            c.Default = &Else{
                Scope: elif.Scope,
            }
            break
        }

        if len(c.Elifs) == 0 {
            ret
        }

        // Remove unreachable cases.
        let mut i = &c.Elifs[0]
        let end = &c.Elifs[len(c.Elifs)-1]
        for i <= end; i++ {
            unsafe {
                if isUnreachableConditionalCase(*i) {
                    *i = nil
                }
            }
        }

        if len(c.Elifs) == 0 {
            ret
        }

        // Skip one-case checking if const-case is not exist.
        if !constCase {
            ret
        }

        let mut only = -1
        for j, elif in c.Elifs {
            if elif != nil {
                if only != -1 {
                    // Break checking, there is more than one case.
                    ret
                }
                only = j
            }
        }

        // Here is one case.
        if only != -1 {
            self.setCurrentStmt(c.Elifs[only].Scope)
        }
    }

    fn buildDefaultConstantTrueCase(mut self, mut case: &Case, mut &i: int, mut &m: &Match) {
        case.Exprs = nil // Remove expressions to remove eval overhead.
        let start = i
    loop:
        for {
            if len(case.Scope.Stmts) == 0 {
                break
            }
            let mut &stmt = unsafe { *(&case.Scope.Stmts[len(case.Scope.Stmts)-1]) }
            match type stmt {
            | &FallSt:
                i++
                if i >= len(m.Cases) {
                    stmt = m.Default.Scope
                    break loop
                }
                case = m.Cases[i]
                stmt = case.Scope
            |:
                break loop
            }
        }
        m.Default = m.Cases[start]
        m.Cases = m.Cases[:start]
    }

    fn optimizeMatch(mut self, mut m: &Match) {
        for (_, mut case) in m.Cases {
            for (_, mut expr) in case.Exprs {
                exprOptimizer.optimize(expr.Model)
            }
            self.optimizeChild(case.Scope)
        }
        if m.Default != nil {
            self.optimizeChild(m.Default.Scope)
        }

        if !Cond {
            ret
        }

        let mut constCase = false // Has cosntant true case.

        // Remove unnecessary trailing cases that comes after constant true case.
        for (mut i, mut case) in m.Cases {
            if !isConstantValidMatchCase(case) {
                continue
            }
            constCase = true
            self.buildDefaultConstantTrueCase(case, i, m)
            break
        }

        if len(m.Cases) == 0 {
            ret
        }

        // Remove unreachable cases.
        let mut i = &m.Cases[0]
        let end = &m.Cases[len(m.Cases)-1]
        for i <= end; i++ {
            unsafe {
                if isUnreachableMatchCase(*i) {
                    *i = nil
                }
            }
        }

        // Skip one-case checking if const-case is not exist.
        if !constCase {
            ret
        }

        let mut only = -1
        for j, case in m.Cases {
            if case != nil {
                if only != -1 {
                    // Break checking, there is more than one case.
                    ret
                }
                only = j
            }
        }

        // Here is one case.
        if only != -1 {
            self.setCurrentStmt(m.Cases[only].Scope)
        }
    }

    fn optimizeRangeIter(mut self, mut it: &RangeIter) {
        exprOptimizer.optimize(it.Expr.Model)
        self.optimizeChild(it.Scope)
    }

    fn optimizeWhileIter(mut self, mut it: &WhileIter) {
        exprOptimizer.optimize(it.Expr)
        self.optimizeStmt(it.Next)
        self.optimizeChild(it.Scope)
    }

    fn substr(mut self, mut a: &Assign): bool {
        if a.Op.Kind != TokenKind.Eq {
            ret false
        }
        match type a.R.Model {
        | &SlicingExprModel:
            let mut sem = (&SlicingExprModel)(a.R.Model)
            if a.L.Model == sem.Expr {
                self.setCurrentStmt(&MutSlicingExprModel{
                    Token: sem.Token,
                    Expr: sem.Expr,
                    Left: sem.Left,
                    Right: sem.Right,
                })
                ret true
            }
        }
        ret false
    }

    fn strInsert(mut self, mut a: &Assign): bool {
        if a.Op.Kind != TokenKind.Eq {
            ret false
        }
        match type a.R.Model {
        | &BinopExprModel:
            let mut m = (&BinopExprModel)(a.R.Model)
            if a.L.Model == m.Right.Model {
                self.setCurrentStmt(&StrInsertBeginExprModel{
                    Dest: a.L.Model,
                    Expr: m.Left.Model,
                })
                ret true
            }
        }
        ret false
    }

    fn strAppend(mut self, mut a: &Assign): bool {
        if a.Op.Kind != TokenKind.PlusEq {
            ret false
        }
        match type a.R.Model {
        | &Const:
            // Constant string literal appending to string.
            self.setCurrentStmt(&StrAppendExprModel{
                Str: a.L.Model,
                Expr: a.R.Model,
            })
            ret true
        | &CastingExprModel:
            // Casting byte literal, rune literal, byte slices and rune slices
            // then appending to string.
            let mut c = (&CastingExprModel)(a.R.Model)
            let mut prim = c.ExprKind.Prim()
            if prim == nil && c.ExprKind.Slc() != nil {
                prim = c.ExprKind.Slc().Elem.Prim()
            }
            if prim == nil || (!prim.IsU8() && !prim.IsI32()) {
                break
            }
            self.setCurrentStmt(&StrAppendExprModel{
                Str: a.L.Model,
                Expr: c.Expr,
            })
            ret true
        }
        ret false
    }

    fn strAssign(mut self, mut a: &Assign): bool {
        if !Str {
            ret false
        }
        let lp = a.L.Kind.Prim()
        if lp == nil || !lp.IsStr() {
            ret false
        }
        match {
        | self.substr(a)
        | self.strInsert(a)
        | self.strAppend(a):
            ret true
        }
        ret false
    }

    fn optimizeAssign(mut self, mut assign: &Assign) {
        if self.strAssign(assign) {
            ret
        }

        exprOptimizer.optimize(assign.L.Model)
        exprOptimizer.optimize(assign.R.Model)

        match assign.Op.Kind {
        | TokenKind.SolidusEq | TokenKind.PercentEq:
            // Do not check division of structures safety.
            if Math && assign.L.Kind.Struct() == nil {
                assign.Op.Kind = assign.Op.Kind[:len(assign.Op.Kind)-1]
                let mut model: ExprModel = &BinopExprModel{
                    Op: assign.Op,
                    Left: assign.L,
                    Right: assign.R,
                }
                exprOptimizer.optimize(model)
                match type model {
                | &BinopExprModel:
                    assign.R = new(OperandExprModel, *assign.R)
                    assign.Op.Kind = TokenKind.Eq
                    assign.R.Model = model
                    ret
                }
                assign.Op.Kind += "="
                ret
            }
        }

        if Append {
            match type assign.R.Model {
            | &BuiltinAppendCallExprModel:
                let mut m = (&BuiltinAppendCallExprModel)(assign.R.Model)
                match type m.Dest {
                | &Var
                | &StructSubIdentExprModel:
                    match type m.Elements {
                    | &SliceExprModel:
                        // Push items one-by-one.
                        self.setCurrentStmt(&PushToSliceExprModel{
                            Dest: m.Dest,
                            Elems: (&SliceExprModel)(m.Elements),
                        })
                    |:
                        // Push directly if appended and assigned to the same memory.
                        if areSameLvalueExprModel(assign.L.Model, m.Dest) {
                            self.setCurrentStmt(&AppendToSliceExprModel{
                                Dest: assign.L.Model,
                                Slice: m.Elements,
                            })
                        }
                    }
                }
            }
        }
    }

    fn optimizeMultiAssign(mut self, mut assign: &MultiAssign) {
        for (_, mut l) in assign.L {
            if l != nil {
                exprOptimizer.optimize(l.Model)
            }
        }
        exprOptimizer.optimize(assign.R)
    }

    fn optimizeRet(mut self, mut r: &RetSt) {
        exprOptimizer.optimize(r.Expr)
    }

    fn optimizeStmt(mut self, mut stmt: Stmt) {
        match type stmt {
        | &Scope:
            self.optimizeChild((&Scope)(stmt))
        | &Data:
            self.optimizeData((&Data)(stmt))
        | &Var:
            self.optimizeVar((&Var)(stmt))
        | &Conditional:
            self.optimizeConditional((&Conditional)(stmt))
        | &RangeIter:
            self.optimizeRangeIter((&RangeIter)(stmt))
        | &WhileIter:
            self.optimizeWhileIter((&WhileIter)(stmt))
        | &InfIter:
            self.optimizeChild((&InfIter)(stmt).Scope)
        | &Assign:
            self.optimizeAssign((&Assign)(stmt))
        | &MultiAssign:
            self.optimizeMultiAssign((&MultiAssign)(stmt))
        | &Match:
            self.optimizeMatch((&Match)(stmt))
        | &RetSt:
            self.optimizeRet((&RetSt)(stmt))
        }
    }

    fn optimizeChild(mut self, mut child: &Scope) {
        let mut so = scopeOptimizer.new(child)
        so.optimize()
    }

    // Optimizes scope by enabled optimizations.
    fn optimize(mut self) {
        for (i, mut stmt) in self.scope.Stmts {
            self.i = i
            self.optimizeStmt(stmt)
        }
    }
}

fn isStdPackage(f: str, p: str): bool {
    ret strings::HasPrefix(f, path::Join(PathStdlib, p))
}

fn isConstantValidConditionalCase(&i: &If): bool {
    match type i.Expr {
    | &Const:
        let c = (&Const)(i.Expr)
        ret c.IsBool() && c.ReadBool()
    }
    ret false
}

fn isUnreachableExpr(&expr: ExprModel): bool {
    match type expr {
    | &Const:
        let c = (&Const)(expr)
        ret c.IsBool() && !c.ReadBool()
    | &BinopExprModel:
        let m = (&BinopExprModel)(expr)
        if m.Op.Kind == TokenKind.DblAmper {
            ret isUnreachableExpr(m.Left.Model) ||
                isUnreachableExpr(m.Right.Model)
        }
    }
    ret false
}

fn isConstantValidMatchCase(&case: &Case): bool {
    for _, expr in case.Exprs {
        if expr.IsConst() && expr.Constant.IsBool() && expr.Constant.ReadBool() {
            ret true
        }
    }
    ret false
}

fn isUnreachableConditionalCase(&i: &If): bool {
    ret isUnreachableExpr(i.Expr)
}

fn isUnreachableMatchCase(&case: &Case): bool {
    for _, expr in case.Exprs {
        if !isUnreachableExpr(expr.Model) {
            ret false
        }
    }
    ret true
}