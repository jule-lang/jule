// Copyright 2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use obj
use sema for std::jule::sema

// Dead code eliminate optimizer for expressions.
struct exprDeadCode {
	mut s: &scopeDeadCode
}

impl exprDeadCode {
	static fn new(mut &s: &scopeDeadCode): exprDeadCode {
		ret exprDeadCode{
			s: s,
		}
	}

	fn optimize(self, mut &model: sema::ExprModel) {
		mut inspector := obj::ExprInspector.New()
		inspector.Inspect(model, fn(mut &expr: sema::ExprModel) {
			match type expr {
			| &sema::FnCallExprModel:
				mut m := (&sema::FnCallExprModel)(expr)
				if m.Except != nil {
					unsafe { self.s.optimizeBodyChildExceptional(m.Except) }
				}
			| &sema::AnonFnExprModel:
				mut m := (&sema::AnonFnExprModel)(expr)
				eliminateDeadCodeOfScope(m.Func.Scope)
			}
		})
	}
}