// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// This is the main package of JuleC.

use env
use std::fs::{File}
use jule for std::jule
use build for std::jule::build
use std::env
use strings for std::strings

// Compiler commands.
const CMD_HELP = "help"
const CMD_VERSION = "version"
const CMD_TOOL = "tool"
const CMD_JULENV = "julenv"
const CMD_MOD = "mod"

// Map for "julec help" command.
static HELP_MAP: [...][2]str = [
    [CMD_HELP, "Show help"],
    [CMD_VERSION, "Show version"],
    [CMD_TOOL, "Tools for effective Jule"],
    [CMD_JULENV, "Show information about native jule environment"],
    [CMD_MOD, "Module management"],
]

fn print_error_message(msg: str) {
    outln(msg)
}

// Command: julec help
fn help(&args: []str) {
    if args.len > 2 {
        print_error_message("invalid command: " + args[2])
        ret
    }

    let mut max = HELP_MAP[0][0].len
    for _, k in HELP_MAP {
        let n = k[0].len
        if n > max {
            max = n
        }
    }

    let mut s = ""
    const SPACE = 5 // Space of between command name and description.
    for i, part in HELP_MAP {
        s += part[0]
        s += strings::repeat(" ", (max - part[0].len) + SPACE)
        s += part[1]
        if i+1 < HELP_MAP.len {
            s += "\n"
        }
    }
    outln(s)
}

// Command: julec version
fn version(&args: []str) {
    if args.len > 2 {
        print_error_message("invalid command: " + args[2])
        ret
    }
    outln(jule::VERSION)
}

// Command: julec tool distos
fn tool_distos() {
    out("supported operating systems:\n ")
    for _, os in build::DISTOS {
        out(os)
        out(" ")
    }
    outln("")
}

// Command: julec tool distarch
fn tool_distarch() {
    out("supported architects:\n ")
    for _, arch in build::DISTARCH {
        out(arch)
        out(" ")
    }
    outln("")
}

// Command: julec tool
fn tool(&args: []str) {
    if args.len == 2 {
        outln(`tool commands:
 distos     Lists all supported operating systems
 distarch   Lists all supported architects`)
        ret
    } else if args.len > 3 {
        print_error_message("invalid command: " + args[3])
        ret
    }

    let cmd = args[2]
    match cmd {
    | "distos":
        tool_distos()
    | "distarch":
        tool_distarch()
    |:
        print_error_message("undefined command: " + cmd)
    }
}

// Command: julec julenv
fn julenv(&args: []str) {
    if args.len > 2 {
        print_error_message("invalid command: " + args[2])
        ret
    }
    outln("julec version: " + jule::VERSION)
    outln("architecture: " + std::env::ARCH)
    outln("operating system: " + std::env::OS)
    outln("default compiler: " + env::COMPILER)
    outln("default C++ standard: " + env::CPP_STD)
}

// Command: julec mod
fn mod(&args: []str) {
    if args.len == 2 {
        outln("command is not given, try julec mod init")
        ret
    }

    if args.len > 3 {
        print_error_message("invalid command: " + args[3])
        ret
    }

    match args[2] {
    | "init": // julec mod init
        File.write(build::MODULE_FILE, [], 0o660) else {
            print_error_message("module could not generated because of a problem")
        }
    |:
        print_error_message("invalid command: " + args[2])
    }
}

// Try to process compiler commands.
// Reports whether "ARGS" is command and processed.
fn process_command(&args: []str): bool {
    match args[1] {
    | CMD_HELP:
        help(args)
    | CMD_VERSION:
        version(args)
    | CMD_TOOL:
        tool(args)
    | CMD_JULENV:
        julenv(args)
    | CMD_MOD:
        mod(args)
    |:
        ret false
    }

    ret true
}

fn show_info() {
    outln(
        `JuleC is a tool for Jule source code and developers.

Commands:
    help          Show help,
    version       Show version,
    tool          Tools for effective Jule,
    julenv        Show information about native jule environment
    mod           Module management

Compilation:
    julec [OPTIONS] INPUT
`)
}

fn main() {
    let mut args = std::env::args()

    // Not started with arguments.
    // Here is "2" but "args" always have one element for store program name.
    if args.len < 2 {
        show_info()
        ret
    }

    if process_command(args) {
        ret
    }

    compile_command(args)
}